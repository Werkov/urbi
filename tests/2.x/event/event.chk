// Check infrastructure under events
//#no-fast
var e = Event.new()|;
var res = []|;

e.trigger(-1, 0)|;
at (e?(var a, var b))
  res << (a + b),
assert { res == [-1] };
e.emit (1, 2);
assert { res == [-1, 3] };
e.emit (3, 4);
assert { res == [-1, 3, 7] };
res.clear()|;
e.emit (5, 6) & e.emit (7, 8);
assert
{
  res.size == 2;
  11 in res;
  15 in res;
};
at (e?(5, var b))
{
  sleep(1s);
  echo (b)
},
e.emit (5, 6) & e.emit (7, 8);
[00000001] *** 6
sleep (1s);


// We have segfaulted on this (issue #4800).
var g = Event.new()|;
at (g?(var v))
  echo("a");
g.trigger().stop();


// Check tag stopped at.
// Note that we might issue 1, or 1 and 2 etc., that would still be right.
// It would probably be better to rely on a counter, and make sure it
// stops counting at some point.  But currently, good enough like this.
var f = Event.new()|;
t: at (f?) { echo(1); echo(2); echo(3) };
f! | t.stop();


echo(4);
[00000001] *** 4
