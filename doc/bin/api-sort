#! /usr/bin/perl -w

BEGIN
{
  use File::Basename;
  unshift @INC, dirname($0) . '/../../build-aux/lib/perl';
}

use strict;
use File::Basename;
use BuildAux::FileUtils;
use BuildAux::Verbose;
use BuildAux::XFile;


=item C<getopt()>

Process the command line options.

=cut

sub getopt ()
{
  use BuildAux::Getopt qw($message);
  $message = "Sort urbiscriptapi entries";
  BuildAux::Getopt::getopt
    (
    );
}

sub normalize($)
{
  my ($s) = @_;
  # Sort "'assert'" as "assert", not at "'".
  $s =~ s/\A'(.*)'\Z/$1/;
  # Sort case-insensitively.
  lc $s
}

sub slot_cmp
{
  (normalize $a) cmp (normalize $b)
}

=item C<transform($file, \$contents)>

Sort the urbiscriptapi sections.

=cut

sub transform ($\$)
{
  my ($file, $contents_ref) = @_;
  my %slot;
  $$contents_ref =~
    s
    <
      \\begin{urbiscriptapi}
      (.*?)
      \\end{urbiscriptapi}
    >
    <
      verbose 1, "Got an urbiscriptapi section";
      my $body = $1;
      $body =~ s{(
                   ^[% ]*\\item
                   (?:\[(?<name>.*?)\]
                     |\s*\\lstinline\|(?<name>.*?)\()
                   .*?
                 )
                 (?=^[% ]*\\item|\z)}
         {
           verbose 1, "Key: $+{name}";
           $slot{$+{name}} = $1;
           ""
         }gexsm;

       # This is what was not recognized.
       $slot{"ZZZZZ"} = $body;

       # Trailing spaces;
       map { s/\s+\z//g } values %slot;

       ("\\begin{urbiscriptapi}\n"
        . join ("\n\n\n",
                map { $slot{$_} } (sort slot_cmp keys %slot))
        . "\n\\end{urbiscriptapi}");
    >xsge;
  $$contents_ref;
}


=item C<transform_file ($file, &transform)>

Apply C<&transform> on the contents of C<$file>.  If there is no
difference, do nothing.  Otherwise, back-up the original C<$file> into
C<$file.bak>, save the new contents in C<$file>, and show the
differences.  Preserve permissions.

=cut

sub transform_file ($$)
{
  my ($file, $transform_ref) = @_;
  my $contents = contents ($file);
  file_update($file, $transform_ref->($file, \$contents));
}


## ------ ##
## Main.  ##
## ------ ##

getopt;
map { transform_file ($_, \&transform); } @ARGV;


### Setup "Gostai" style for perl-mode and cperl-mode.
## Local Variables:
## perl-indent-level: 2
## perl-continued-statement-offset: 2
## perl-continued-brace-offset: -2
## perl-brace-offset: 0
## perl-brace-imaginary-offset: 0
## perl-label-offset: -2
## cperl-indent-level: 2
## cperl-brace-offset: 0
## cperl-continued-brace-offset: -2
## cperl-label-offset: -2
## cperl-extra-newline-before-brace: t
## cperl-merge-trailing-else: nil
## cperl-continued-statement-offset: 2
## End:
