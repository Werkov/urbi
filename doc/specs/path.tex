%% Copyright (C) 2009-2011, Gostai S.A.S.
%%
%% This software is provided "as is" without warranty of any kind,
%% either expressed or implied, including but not limited to the
%% implied warranties of fitness for a particular purpose.
%%
%% See the LICENSE file for more information.

\section{Path}

A \dfn{Path} points to a file system entity (directory, file and so
forth).

\subsection{Prototypes}
\begin{refObjects}
\item[Comparable]
\item[Orderable]
\end{refObjects}

\subsection{Construction}

Path itself is the root of the file system: \lstinline|/| on Unix, and
\lstinline|C:\| on Windows.

\begin{urbiscript}[firstnumber=1]
Path;
[00000001] Path("/")
\end{urbiscript}
\begin{urbicomment}
skipIfWindows();
\end{urbicomment}

A \lstinline|Path| is constructed with the string that points to the file
system entity.  This path can be relative or absolute.

\begin{urbiscript}[firstnumber=1]
Path.new("foo");
[00000002] Path("foo")

Path.new("/path/file.u");
[00000001] Path("/path/file.u")
\end{urbiscript}

Some minor simplifications are made, such as stripping useless
\file{./} occurrences.

\begin{urbiscript}
Path.new("././///.//foo/");
[00000002] Path("foo")
\end{urbiscript}

\subsection{Slots}
\begin{urbiscriptapi}
\item['/'](<rhs>)%
  Create a new \dfn{Path} that is the concatenation of
  \this and \lstinline|\var{rhs}|. \lstinline|\var{rhs}|
  can be a \dfn{Path} or a \dfn{String} and cannot be absolute.
\begin{urbiscript}
assert(Path.new("/foo/bar") / Path.new("baz/qux/quux")
       == Path.new("/foo/bar/baz/qux/quux"));
Path.cwd / Path.new("/tmp/foo");
[00000003:error] !!! /: Rhs of concatenation is absolute: /tmp/foo
\end{urbiscript}


\item['<'](<that>)%
  Same as comparing the string versions of \this and
  \var{that}.
\begin{urbiassert}
  Path.new("/a")   < Path.new("/a/b");
!(Path.new("/a/b") < Path.new("/a")  );
\end{urbiassert}


\item['=='](<that>)%
  Same as comparing the string versions of \this and
  \var{that}.  Beware that two paths may be different and point to the
  very same location.
\begin{urbiassert}
  Path.new("/a")  == Path.new("/a");
!(Path.new("/a")  == Path.new("a")  );
\end{urbiassert}


\item[absolute]
  Whether \this is absolute.
\begin{urbiassert}
Path.new("/abs/path").absolute;
!Path.new("rel/path").absolute;
\end{urbiassert}


\item[asList]
  List of names used in path (directories and possibly file), from
  bottom up. There is no difference between relative path and absolute
  path.
\begin{urbiassert}
Path.new("/path/to/file.u").asList() == ["path", "to", "file.u"];
Path.new("/path").asList()           == Path.new("path").asList();
\end{urbiassert}


\item[asPrintable]
\begin{urbiassert}
Path.new("file.txt").asPrintable() == "Path(\"file.txt\")";
\end{urbiassert}


\item[asString]
  The name of the file.
\begin{urbiassert}
Path.new("file.txt").asString() == "file.txt";
\end{urbiassert}


\item[basename]%
  Base name of the path.  See also \refSlot{dirname}.
\begin{urbiassert}
Path.new("/absolute/path/file.u").basename == "file.u";
Path.new("relative/path/file.u").basename  == "file.u";
\end{urbiassert}


\item[cd]%
  Change the current working directory to \this. Return the new current
  working directory as a \lstinline|Path|.
\begin{urbiassert}
var a = Directory.create("a").asPath();
var b = Directory.create("a/b").asPath();
var cwd = Path.cwd;     // Current location.
cwd.isA(Path);
// cd returns the new current working directory.
b.cd() == cwd / b == cwd / "a" / "b";
Path.cwd == cwd / b;
// Go back to the original location.
Path.new("../..").cd() == cwd;
Path.cwd == cwd;
\end{urbiassert}
\begin{urbicomment}
removeFs("a");
\end{urbicomment}

Exceptions are thrown on cases of error.
\begin{urbiscript}
Path.new("does/not/exist").cd();
[00003991:error] !!! cd: no such file or directory: does/not/exist

var f = File.create("file.txt")|;
f.asPath().cd();
[00099415:error] !!! cd: not a directory: file.txt
\end{urbiscript}
\begin{urbicomment}
removeFs(f);
removeSlots("f");
\end{urbicomment}

Permissions are not properly handled on Windows, so the following example
would actually fail.
\begin{urbiscript}[firstnumber=1]
var d = Directory.create("forbidden")|;
System.system("chmod 444 %s" % d)|;
d.asPath().cd();
[00140753:error] !!! cd: Permission denied: forbidden
\end{urbiscript}
\begin{urbicomment}
skipIfWindows();
removeFs(d);
removeSlots("d");
\end{urbicomment}

\item[cwd]%
  The current working directory.
% We used to write
% assert(Path.new("/").cd == Path.new("/"));
% assert(Path.cwd         == Path.new("/"));
% which is wrong on Windows, because cwd (like cd) returns Z:/ instead
% of /.
\begin{urbiassert}[firstnumber=1]
// Save current directory.
var pwd = Path.cwd;
// Go into "/".
var root = Path.new("/").cd();
// Current working directory is "/".
Path.cwd == root;
// Go back to the directory we were in.
pwd.cd() == pwd;
\end{urbiassert}

\item[dirname]%
  Directory name of the path.  See also \refSlot{basename}.
\begin{urbiassert}
Path.new("/abs/path/file.u").dirname == Path.new("/abs/path");
Path.new("rel/path/file.u").dirname  == Path.new("rel/path");
\end{urbiassert}


\item[exists]%
  Whether something (a \refSlot{File}, a \refSlot{Directory}, \ldots) exists
  where \this points to.
\begin{urbiassert}
Path.cwd.exists;
Path.new("/").exists;
var p = Path.new("file.txt");
!p.exists;
File.create(p);
p.exists;
\end{urbiassert}
\begin{urbicomment}
removeFs("file.txt");
\end{urbicomment}


\item[isDir]%
  Whether \this is a directory.
\begin{urbiassert}
Path.cwd.isDir;
var f = File.create("file.txt");
!f.asPath().isDir;
!Path.new("does/not/exist").isDir;
\end{urbiassert}
\begin{urbicomment}
removeFs("file.txt");
\end{urbicomment}


\item[isReg]%
  Whether \this is a regular file.
\begin{urbiassert}
var f = File.create("file.txt");
 f.asPath().isReg;
!Path.cwd.isReg;
!Path.new("does/not/exist").isReg;
\end{urbiassert}
\begin{urbicomment}
removeFs("file.txt");
\end{urbicomment}


\item[lastModifiedDate]%
  Last modified date of the path.
\begin{urbiassert}
var p = Path.new("test");
File.create(p);
0 <= Date.now - p.lastModifiedDate <= 5s;
\end{urbiassert}


\item[open] Open \this. Return either a \dfn{Directory} or a \dfn{File}
  according the type of \this. See \refObject{File} and
  \refObject{Directory}.
\begin{urbiassert}
Path.new("/").open().isA(Directory);
\end{urbiassert}


\item[readable]
  Whether \this is readable.  Throw if does not even exist.  On Windows,
  always returns true.
\begin{urbiassert}[firstnumber=1]
Path.new(".").readable;
var p = Path.new("file.txt");
File.create(p);
p.readable;
System.system("chmod a-r %s" % p) == 0;
!p.readable;
System.system("chmod a+r %s" % p) == 0;
p.readable;
\end{urbiassert}
\begin{urbicomment}
skipIfWindows();
removeFs("file.txt");
\end{urbicomment}


\item[rename](<name>)%
  Rename the file-system object (directory, file, etc.) pointed to by \this,
  as \var{name}.  Return \refObject{void}.
\begin{urbiassert}[firstnumber=1]
var dir1 = Directory.create("dir1");
var p = dir1.asPath();
p.rename("dir2").isVoid;
p.basename == "dir2";
\end{urbiassert}
\begin{urbicomment}
removeFs("dir2");
\end{urbicomment}


\item[writable]%
  Whether \this is writable.  Throw if does not even exist.  On Windows,
  always returns true.
\begin{urbiassert}[firstnumber=1]
Path.new(".").writable;
var p = Path.new("file.txt");
File.create(p);
p.writable;
System.system("chmod a-w %s" % p) == 0;
!p.writable;
System.system("chmod a+w %s" % p) == 0;
p.writable;
\end{urbiassert}
\begin{urbicomment}
skipIfWindows();
removeFs("file.txt");
\end{urbicomment}
\end{urbiscriptapi}


%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: "../urbi-sdk"
%%% ispell-dictionary: "american"
%%% ispell-personal-dictionary: "../urbi.dict"
%%% fill-column: 76
%%% End:
