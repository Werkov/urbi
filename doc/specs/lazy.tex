\section{Lazy}

\dfn{Lazies} are objects that hold a lazy value, that is, a not yet evaluated
value. They provide facilities to evaluate their content only once
(\dfn{memoization}) or several times. Lazy are essentially used in call
messages, to represent lazy arguments, as described in
\autorefObject{CallMessage}.

\subsection{Construction}

Lazies are seldom instantiated manually. They are mainly created
automatically when a lazy function call is made (see
\autoref{sec:us-fun-callmsg}). One can however create a lazy value with the
standard \lstinline|new| method of \lstinline|Lazy|, giving it an
argument-less function which evaluates to the lazified value.

\begin{urbiscript}
Lazy.new(closure () { /* Value to lazify */ });
[00000000:hide] Lazy_0xADDR
\end{urbiscript}

\subsection{Methods}

\begin{itemize}
\item \lstinline|eval|\\
The \lstinline|eval| method forces evaluation of the held lazy
value. Two calls to \lstinline|eval| will systematically evaluate the
expression twice, which can be useful to duplicates its side effects.

\item \lstinline|value|\\
The \lstinline|value| method returns the held value, potentially
evaluating it before. \lstinline|value| performs memoization, that is,
only the first call will actually evaluate the expression, subsequent
calls will return the cached value. Unless you want to explicitly
trigger side effects from the expression by evaluating it several
time, this should be preferred over \lstinline|eval| to avoid
evaluating the expression several times uselessly.

\end{itemize}

\subsection{Examples}

\subsubsection{Evaluating once}

One usage of lazy values is to avoid evaluating an expression unless
it's actually needed, because it's expensive or has undesired side
effects. The listing below presents a situation where an
expensive-to-compute value (\lstinline|heavy_computation|) might be
needed zero, one or two times. The objective is to save time by:

\begin{itemize}
\item Not evaluating it if it's not needed.
\item Evaluating it only once if it's needed one or two time.
\end{itemize}

We thus make the wanted expression lazy, and use the \lstinline|value|
method to fetch its value when needed.

\begin{urbiscript}
// This function supposedly performs expensive computations.
function heavy_computation()
{
  echo("Heavy computation");
  return 1 + 1;
};
[00000000:hide] function () {
[:]  echo("Heavy computation");
[:]  return 1 . '+'(1);
[:]}

// We want to do the heavy computations only if needed,
// and make it a lazy value to be able to evaluate it "on demand".
var v = Lazy.new(closure () { heavy_computation() });
[00000000] Lazy_0xADDR
/* some code */;
// So far, the value was not needed, and heavy_computation
// was not evaluated.
/* some code */;
// If the value is needed, heavy_computation is evaluated.
v.value;
[00000000] *** Heavy computation
[00000000] 2
// If the value is needed a second time, heavy_computation
// is not reevaluated.
v.value;
[00000000] 2
\end{urbiscript}

\subsubsection{Evaluating several times}

Evaluating a lazy several times only makes sense with lazy arguments
and call messages. See example with call messages in
\autoref{sec:std-callmsg-examples-several}.


\subsection{Caching}

\refObject{Lazy} is meant for functions without argument.  If you need
\dfn{caching} for functions that depend on arguments, it is
straightforward to implement using a \refObject{Dictionary}.  In the
future \us might support dictionaries whose indices are not only
strings, but in the meanwhile, you have to convert the arguments into
strings, as the following sample object demonstrates.

\begin{urbiscript}
class UnaryLazy
{
  function init(f)
  {
    results = Dictionary.new;
    func = f;
  };
  function value(p)
  {
    var sp = p.asString;
    if (results.has(sp))
      return results[sp];
    var res = func(p);
    results[sp] = res |
    res
  };
  var results;
  var func;
} |
// The function to cache.
var inc = function(x) { echo("incing " + x) | x+1 } |
// The function with cache.
// Use "getSlot" to get the unevaluated function.
var p = UnaryLazy.new(getSlot("inc"));
[00062847] UnaryLazy_0x78b750
p.value(1);
[00066758] *** incing 1
[00066759] 2
p.value(1);
[00069058] 2
p.value(2);
[00071558] *** incing 2
[00071559] 3
p.value(2);
[00072762] 3
p.value(1);
[00074562] 2
\end{urbiscript}


%%% Local Variables:
%%% mode: latex
%%% TeX-master: "../urbi-sdk"
%%% End:
