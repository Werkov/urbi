%% Copyright (C) 2009-2011, Gostai S.A.S.
%%
%% This software is provided "as is" without warranty of any kind,
%% either expressed or implied, including but not limited to the
%% implied warranties of fitness for a particular purpose.
%%
%% See the LICENSE file for more information.

\section{Group}
A transparent means to send messages to several objects as if they
were one.

\subsection{Example}

The following session demonstrates the features of the Group
objects.  It first creates the \lstinline|Sample| family of object,
makes a group of such object, and uses that group.

\begin{urbiscript}[firstnumber=1]
class Sample
{
  var value = 0;
  function init(v)    { value = v; };
  function asString() { "<" + value.asString() + ">"; };
  function timesTen() { new(value * 10); };
  function plusTwo()  { new(value + 2); };
};
[00000000] <0>

var group = Group.new(Sample.new(1), Sample.new(2));
[00000000] Group [<1>, <2>]
group << Sample.new(3);
[00000000] Group [<1>, <2>, <3>]
group.timesTen.plusTwo;
[00000000] Group [<12>, <22>, <32>]

// Bouncing getSlot and updateSlot.
group.value;
[00000000] Group [1, 2, 3]
group.value = 10;
[00000000] Group [10, 10, 10]

// Bouncing to each&.
var sum = 0|
for& (var v : group)
  sum += v.value;
sum;
[00000000] 30
\end{urbiscript}

\subsection{Prototypes}

\begin{refObjects}
\item[RangeIterable]
\item[Comparable]
\end{refObjects}

\subsection{Construction}

Groups are created like any other object. The constructor can take members
to add to the group.

\begin{urbiscript}
Group.new();
[00000000] Group []
Group.new(1, "two");
[00000000] Group [1, "two"]
\end{urbiscript}

\subsection{Slots}

\begin{urbiscriptapi}
\item['<<'](<member>)%
  Syntactic sugar for \refSlot{add}.


\item['=='](<that>)%
  Whether \lstinline|this.members == \var{that}.members|.
\begin{urbiassert}
               Group.new() == Group.new();
Group.new(1, [2], "foo") == Group.new(1, [2], "foo");
         Group.new(1, 2) != Group.new(2, 1);
            Group.new(1) != Group.new(2);
\end{urbiassert}


\item[add](<member>, ...)%
  Add members to \this group, and return \this.
\begin{urbiassert}
var g = Group.new(1, 2);
g.add(3, 4) === g;
g.members == [1, 2, 3, 4];
\end{urbiassert}


\item[asString]
  Report the \lstinline|asString| of the members.
\begin{urbiassert}
Group.new(1, 2).asString() == "Group [1, 2]";
\end{urbiassert}


\item[each](<action>)%
  Apply \var{action} to all the members, in sequence, then return the
  Group of the results, in the same order.  Allows to iterate over a
  Group via \lstinline|for|.


\item['each&'](<action>)%
  Apply \var{action} to all the members, concurrently, then return the
  Group of the results.  The order is \emph{not} necessarily the same.
  Allows to iterate over a Group via \lstinline|for&|.


\item[fallback]
  This function is called when a method call on \this
  failed.  It bounces the call to the members of the group, collects
  the results returned as a group.  This allows to chain grouped
  operation in a row.  If the dispatched calls return
  \lstinline|void|, returns a single \lstinline|void|, not a ``group
  of \lstinline|void|''.


\item[getProperty](<slot>, <prop>)%
  Bounced to the members so that \lstinline|this.\var{slot}->\var{prop}|
  actually collects the values of the property \var{prop} of the slots
  \var{slot} of the group members.  See \refSlot[Object]{getProperty}.
\begin{urbiscript}
class C
{
  var val = 0;
}|;

var a = C.new()|; var b = C.new()|;
var g = Group.new() << a << b|;

g.val->prop;
[00010640:error] !!! property lookup failed: val->prop

a.val->prop = 42|;
g.val->prop;
[00010640:error] !!! property lookup failed: val->prop

b.val->prop = 51|;
assert
{
  g.val->prop == Group.new(42, 51);
};
\end{urbiscript}
\begin{urbicomment}
  removeSlots("C", "a", "b", "g");
\end{urbicomment}


\item[hasProperty](<slot>, <prop>)%
  Bounce to the members and return a group for the results.  See
  \refSlot[Object]{hasProperty}.
\begin{urbiscript}
class C
{
  var val = 0;
}|;

var a = C.new()|; var b = C.new()|;
var g = Group.new() << a << b|;

assert
{
  g.hasProperty("val", "prop") == Group.new(false, false);
  a.val->prop = 21;
  g.hasProperty("val", "prop") == Group.new(true, false);
  b.val->prop = 42;
  g.hasProperty("val", "prop") == Group.new(true, true);
};
\end{urbiscript}
\begin{urbicomment}
  removeSlots("C", "a", "b", "g");
\end{urbicomment}


\item[hasSlot](<name>)%
  True if and only if all the members have the slot.

\begin{urbiassert}
var g = Group.new(1, 2);

!g.hasSlot("foo");
 g.hasSlot("+");
 g + 1 == Group.new(2, 3);
\end{urbiassert}


\item[remove](<member>, ...)%
  Remove members from \this group, and return \this.  Non-existing members
  are silently ignored.
\begin{urbiassert}
var g = Group.new(1, 2, 1);
g.remove(1, 3) === g == Group.new(2);
g.remove(2)    === g == Group.new();
\end{urbiassert}


\item[setProperty](<slot>, <prop>, <value>)%
  Bounced to the members so that
  \lstinline|this.\var{slot}->\var{prop} = \var{value}| actually updates the
  value of the property \var{prop} in the slots \var{slot} of the group
  members, and return a group for the collected result.  See
  \refSlot[Object]{setProperty}.

\begin{urbiscript}
class C
{
  var val = 0;
}|;

var g = Group.new() << C.new() << C.new()|;

assert
{
  (g.val->prop = 31) == Group.new(31, 31);
};
\end{urbiscript}
\begin{urbicomment}
  removeSlots("C", "g");
\end{urbicomment}


\item[updateSlot](<name>, <value>)%
  Bounced to the members so that
  \lstinline|this.\var{name} = \var{value}|
  actually updates the value of the slot \var{name} in
  the group members.
\end{urbiscriptapi}

%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: "../urbi-sdk"
%%% ispell-dictionary: "american"
%%% ispell-personal-dictionary: "../urbi.dict"
%%% fill-column: 76
%%% End:
