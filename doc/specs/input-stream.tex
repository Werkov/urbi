%% Copyright (C) 2009-2011, Gostai S.A.S.
%%
%% This software is provided "as is" without warranty of any kind,
%% either expressed or implied, including but not limited to the
%% implied warranties of fitness for a particular purpose.
%%
%% See the LICENSE file for more information.

\section{InputStream}

InputStreams are used to read (possibly binary) files by hand.
\refObject{File} provides means to swallow a whole file either as a
single large string, or a list of lines.  \lstinline|InputStream|
provides a more fine-grained interface to read files.

\subsection{Prototypes}
\begin{refObjects}
\item[Stream]
\end{refObjects}

\begin{windows}
  Beware that because of limitations in the current implementation,
  one cannot safely read from two different files at the same time
  under Windows.
\end{windows}

\subsection{Construction}

An InputStream is a reading-interface to a file, so its constructor
requires a \refObject{File}.

\begin{urbiscript}[firstnumber=1]
InputStream.new(0);
[00000919:error] !!! new: argument 1: unexpected 0, expected a File

File.save("file.txt", "1\n2\n");
var i1 = InputStream.new(File.new("file.txt"));
[00001208] InputStream_0x1046d16e0
\end{urbiscript}

Cloning a closed \refSlot{Stream} is valid, but it is forbidden to clone an
opened one.

\begin{urbiscript}
var i2 = InputStream.clone.new(File.new("file.txt"));
[00001211] InputStream_0x1045f6760

i1.clone;
[00001288:error] !!! clone: cannot clone opened Streams
\end{urbiscript}

Do not forget to close the streams you opened
(\autoref{sec:specs:Stream:ctor}).

\begin{urbiscript}
i1.close;
i2.close;
\end{urbiscript}

\subsection{Slots}

\begin{urbiscriptapi}
\item[asList]%
  Get the remainder as a \refObject{List}, and an empty \refObject{List}
  if there is nothing left in the stream. Raise an error if the file is closed.
\begin{urbiscript}
var f = "file.txt" |
File.save(f, "1\n2\n") |
var i = InputStream.new(File.new(f)) |
assert (["1", "2"] == i.asList);
assert ([] == i.asList);
i.close;
i.asList;
[00000001:error] !!! asList: stream is closed
\end{urbiscript}
\begin{urbicomment}
removeFs(f);
removeSlots("i", "f");
\end{urbicomment}

\item[content]%
  Get the remainder as a \refObject{String}, or an empty \refObject{String} if
  there is nothing left in the stream. Raise an error if the file is closed.
\begin{urbiscript}
var f = "file.txt" |
File.save(f, "1\000\n2\n") |
var i = InputStream.new(File.new(f)) |
assert (File.new(f).content.data == i.content);
assert ("" == i.content);
i.close;
i.content;
[00000001:error] !!! content: stream is closed
\end{urbiscript}
\begin{urbicomment}
removeFs(f);
removeSlots("i", "f");
\end{urbicomment}

\item[get]%
  Get the next available byte as a \refObject{Float}, or \refObject{nil} if
  the end of file was reached.  Raise an error if the file is closed.
\begin{urbiscript}
File.save("file.txt", "1\n2\n")|;
var i = InputStream.new(File.new("file.txt"))|;
var x;
while (!(x = i.get).isNil)
  cout << x;
[00000001:output] 49
[00000002:output] 10
[00000003:output] 50
[00000004:output] 10

i.close;

i.get;
[00000005:error] !!! get: stream is closed
\end{urbiscript}
\begin{urbicomment}
removeSlots("i", "x");
\end{urbicomment}


\item[getChar]%
  Get the next available byte as a \refObject{String}, or \refObject{nil}
  if the end of file was reached.  Raise an error if the file is closed.
\begin{urbiscript}
File.save("file.txt", "1\n2\n")|;
var i = InputStream.new(File.new("file.txt"))|;
var x;
while (!(x = i.getChar).isNil)
  cout << x;
[00000001:output] "1"
[00000002:output] "\n"
[00000003:output] "2"
[00000004:output] "\n"

i.close;

i.getChar;
[00000005:error] !!! getChar: stream is closed
\end{urbiscript}
\begin{urbicomment}
removeSlots("i", "x");
\end{urbicomment}


\item[getLine]%
  Get the next available line as a \refObject{String}, or \refObject{nil} if
  the end of file was reached.  The end-of-line characters are trimmed.
  Raise an error if the file is closed.
\begin{urbiscript}
File.save("file.txt", "1\n2\n")|;
var i = InputStream.new(File.new("file.txt"))|;
var x;
while (!(x = i.getLine).isNil)
  cout << x;
[00000001:output] "1"
[00000002:output] "2"

i.close;

i.getLine;
[00000005:error] !!! getLine: stream is closed
\end{urbiscript}
\begin{urbicomment}
removeSlots("i", "x");
\end{urbicomment}
\end{urbiscriptapi}


%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: "../urbi-sdk"
%%% ispell-dictionary: "american"
%%% ispell-personal-dictionary: "../urbi.dict"
%%% fill-column: 76
%%% End:
