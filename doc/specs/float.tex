%% Copyright (C) 2008-2011, Gostai S.A.S.
%%
%% This software is provided "as is" without warranty of any kind,
%% either expressed or implied, including but not limited to the
%% implied warranties of fitness for a particular purpose.
%%
%% See the LICENSE file for more information.

\section{Float}

A Float is a floating point number.  It is also used, in the current
version of \us, to represent integers.

\subsection{Prototypes}

\begin{refObjects}
\item[Comparable]
\begin{urbiassert}
!(0 != 0) ; 0 != 1; 1 != 0;
\end{urbiassert}
\item[Orderable]
\begin{urbiassert}
  0 <= 0;    0 <= 1;  !(1 <= 0);
!(0 >  0); !(0 >  1);   1 >  0;
  0 >= 0;  !(0 >= 1);   1 >= 0;
\end{urbiassert}
\item[RangeIterable]
\end{refObjects}

\subsection{Construction}
\label{sec:float:ctor}

The most common way to create fresh floats is using the literal
syntax.  Numbers are composed of three parts:
\begin{description}
\item[integral] (mandatory) a non empty sequence of (decimal) digits;
\item[fractional] (optional) a period, and a non empty sequence of
  (decimal) digits;
\item[exponent] (optional) either \samp{e} or \samp{E}, an optional
  sign (\samp{+} or \samp{-}), then a non-empty sequence of digits.
\end{description}

In other words, float literals match the
\lstinline|[0-9]+(\.[0-9]+)?([eE][-+]?[0-9]+)?| regular expression.  For
instance:

\begin{urbicomment}
import Math.*;
\end{urbicomment}

\begin{urbiassert}
0 == 0000.0000;
// This is actually a call to the unary '+'.
+1 == 1;
0.123456 == 123456 / 1000000;
1e3 == 1000;
1e-3 == 0.001;
1.234e3 == 1234;
\end{urbiassert}

Actually, underscores can be inserted between digits to improve legibility.

\begin{urbiassert}
123_456.78_90 == 1234567890 / 10_000;
\end{urbiassert}

There are also some special numbers, \refSlot{nan}, \refSlot{inf}.

\begin{urbiassert}
Math.log(0) == -inf;
Math.exp(-inf) == 0;
(inf/inf).isNan;
\end{urbiassert}

A null float can also be obtained with \lstinline|new|.

\begin{urbiassert}
Float.new == 0;
\end{urbiassert}

\subsection{Slots}

\begin{urbiscriptapi}
\item \lstinline|'%'(\var{that})|\\
  \this modulo \var{that}.
\begin{urbiassert}
50 % 11 == 6;
\end{urbiassert}


\item['*'](<that>)%
  Product of \this by \var{that}.
\begin{urbiassert}
2 * 3 == 6;
\end{urbiassert}


\item['**'](<that>)%
  \this to the \var{that} power (${this}^{that}$).  The infix exponential
  operator is right-associative (\autoref{sec:lang:op:arith}).
\begin{urbiassert}
 2 ** 3 ** 2 == 2 ** (3 ** 2) != (2 ** 3) ** 2;
 2 ** 10 ==  1024;
 2 ** 31 ==  2_147_483_648;
-2 ** 31 == -2_147_483_648 == -(2**31);
 2 ** 32 ==  4_294_967_296;
-2 ** 32 == -4_294_967_296 == -(2**32);
\end{urbiassert}


\item['+'](<that>)%
  The sum of \this and \var{that}.
\begin{urbiassert}
1 + 1 == 2;
\end{urbiassert}


\item['-'](<that>)%
  \this subtracted by \var{that}.
\begin{urbiassert}
6 - 3 == 3;
\end{urbiassert}


\item['/'](<that>)%
  The quotient of \this divided by \var{that}.
\begin{urbiassert}
50 / 10 == 5;  10 / 50 == 0.2;
\end{urbiassert}


\item['<'](<that>)%
  Whether \this is less than \var{that}. The other comparison operators
  (\lstinline|<=|, \lstinline|>|, \ldots) can thus also be applied on floats
  since Float inherits \refObject{Orderable}.
\begin{urbiassert}
  0 < 1;  !(1 < 0);  !(0 < 0);
\end{urbiassert}


\item['<<'](<that>)%>>
  \this shifted by \var{that} bits towards the left.
\begin{urbiassert}
4 << 2 == 16;
\end{urbiassert}


\item['=='](<that>)%
  Whether \this equals \var{that}.
\begin{urbiassert}
  1 == 1;
!(1 == 2); !(2 == 1);
\end{urbiassert}


\item['>>'](<that>)
  \this shifted by \var{that} bits towards the right.
\begin{urbiassert}
4 >> 2 == 1;
\end{urbiassert}


\item['^'](<that>)%
  Bitwise \dfn{exclusive or} between \this and \var{that}.
\begin{urbiassert}
3 ^ 6 == 5;
\end{urbiassert}


\item[abs]
  Absolute value of the target.
\begin{urbiassert}
(-5).abs == 5 == 5.abs;  0.abs == 0;
\end{urbiassert}


\item[acos]
  Arccosine of the target.
\begin{urbiassert}
0.acos == Float.pi/2;
1.acos == 0;
\end{urbiassert}


\item[asBool]
  Whether non null.
\begin{urbiassert}
     0.asBool === false;
   0.1.asBool === true;
(-0.1).asBool === true;
   Math.inf.asBool === true;
   Math.nan.asBool === true;
\end{urbiassert}


\item[asFloat]
  \this.
\begin{urbiassert}
var x = 51;
x.asFloat === x;
\end{urbiassert}


\item[asin]
  Arcsine of the target.
\begin{urbiassert}
0.asin == 0;
\end{urbiassert}


\item[asList] Bounce to \refSlot{seq}.
\begin{urbiassert}
0.asList == [];  3.asList == [0, 1, 2];
\end{urbiassert}


\item[asString] A \refObject{String} denoting \this.
\begin{urbiassert}
                          42.asString == "42";
                       42.51.asString == "42.51";
              21_474_836_470.asString == "21474836470";
   4_611_686_018_427_387_904.asString == "4611686018427387904";
(-4_611_686_018_427_387_904).asString == "-4611686018427387904";
\end{urbiassert}


\item[atan] Arctangent of \this.
\begin{urbiassert}
0.atan == 0;
1.atan == Float.pi/4;
\end{urbiassert}


\item['bitand'](<that>)%
  The bitwise-and between \this and \var{that}.
\begin{urbiassert}
3 bitand 6 == 2;
\end{urbiassert}


\item['bitor'](<that>)%
  Bitwise-or between \this and \var{that}.
\begin{urbiassert}
3 bitor 6 == 7;
\end{urbiassert}


\item[ceil] The smallest integral value greater than or equal to \this.  See
  also \refSlot{floor}, \refSlot{round}, and \refSlot{trunc}.
\begin{urbiassert}
     0.ceil ==  0;
   1.4.ceil ==  2;     1.5.ceil ==  2;    1.6.ceil ==  2;
(-1.4).ceil == -1;  (-1.5).ceil == -1; (-1.6).ceil == -1;
   inf.ceil == inf; (-inf).ceil == -inf;
   nan.ceil.isNan;
\end{urbiassert}


\item[clone]
  Return a fresh \refObject{Float} with the same value as the target.
\begin{urbiassert}
var x = 0;
var y = x.clone;
x == y;  x !== y;
\end{urbiassert}


\item['compl']
  The complement to 1 of the target interpreted as a 32-bit integer.
\begin{urbiassert}
compl 0 == 0xFFFF_FFFF;    compl 0xFFFF_FFFF == 0;
\end{urbiassert}


\item[cos]
  Cosine of \this.
\begin{urbiassert}
0.cos == 1;   Float.pi.cos == -1;
\end{urbiassert}


\item[each](<fun>)%
  Call the functional argument \var{fun} on every integer from 0 to
  target - 1, sequentially.  The number must be non-negative.
\begin{urbiassert}
{
  var res = [];
  3.each(function (i) { res << 100 + i });
  res
}
== [100, 101, 102];

{
  var res = [];
  for(var x : 3) { res << x; sleep(20ms); res << (100 + x); };
  res
}
== [0, 100, 1, 101, 2, 102];

{
  var res = [];
  0.each (function (i) { res << 100 + i });
  res
}
== [];
\end{urbiassert}


\item['each&'](<fun>)%
  Call the functional argument \var{fun} on every integer from 0 to
  target - 1, concurrently.  The number must be non-negative.
\begin{urbiassert}
{
  var res = [];
  for& (var x : 3) { res << x; sleep(30ms); res << (100 + x) };
  res
}
== [0, 1, 2, 100, 101, 102];
\end{urbiassert}%>>>>


\item[exp]
  Exponential of the target.
\begin{urbiassert}
0.exp == 1;
1.exp ~= 2.71828;
\end{urbiassert}


\item[floor] The largest integral value less than or equal to \this.  See
  also \refSlot{ceil}, \refSlot{round}, and \refSlot{trunc}.
\begin{urbiassert}
     0.floor ==  0;
   1.4.floor ==  1;     1.5.floor ==  1;    1.6.floor ==  1;
(-1.4).floor == -2;  (-1.5).floor == -2; (-1.6).floor == -2;
   Math.inf.floor == Math.inf; (-Math.inf).floor == -inf;
   Math.nan.floor.isNan;
\end{urbiassert}


\item[format](<finfo>)%
  Format according to the \refObject{FormatInfo} object \var{finfo}.
  The precision, \lstinline|\var{finfo}.precision|, sets the maximum
  number of digits after decimal point when in fixed or scientific
  mode, and in total when in default mode.  Beware that 0 plays a
  special role, as it is not a ``significant'' digit.

  \begin{windows}
    Under Windows the behavior differs slightly.
  \end{windows}
\begin{urbiassert}
"%1.0d" % 0.1 == "0.1";
"%1.0d" % 1.1 == {if (System.Platform.isWindows) "1.1" else "1"};

"%1.0f" % 0.1 == "0";
"%1.0f" % 1.1 == "1";
\end{urbiassert}

  Conversion to hexadecimal requires \this to be integral.
\begin{urbiassert}
"%x" % 42 == "2a";
"%x" % 0xFFFF == "ffff";

"%x" % 0.5;
[00000005:error] !!! %: expected integer: 0.5
\end{urbiassert}


\item[fresh]%
  Return a new integer at each call.
\begin{urbiscript}
{
  var res = [];
  for (var i: 10)
    res << Float.fresh;
  assert (res == [1, 2, 3, 4, 5, 6, 7, 8, 9, 10]);
  res = [];
  for (var i: 10)
    res << Float.fresh;
  assert (res == [11, 12, 13, 14, 15, 16, 17, 18, 19, 20]);
};
\end{urbiscript}


\item[hash] A \refObject{Hash} object corresponding to this float value.
  Equal floats (in the sense of \refSlot{'=='}) have equal hashes.  See
  \refSlot[Object]{hash}.

\begin{urbiassert}
0.hash.isA(Hash);
0.hash == 0.hash != 1.hash;
\end{urbiassert}

%%% FIXME: This message is not right, it should be:
%%% [00000005:error] !!! hex: expected integer: 0.5


\item[hex] A \refObject{String} with the conversion of \this in hexadecimal.
  Requires \this to be integral.
\begin{urbiassert}
          0.hex == "0";
       0xFF.hex == "ff";
     0xFFFF.hex == "ffff";
      65535.hex == "ffff";
0xffff_ffff.hex == "ffffffff";

0.5.hex;
[00000005:error] !!! format: expected integer: 0.5
\end{urbiassert}


\item[inf]
  The infinity.
\begin{urbiassert}
2**2**2**2**2 == inf;
inf != -inf;
inf == - -inf;
inf * inf == inf;
inf * -inf == -inf;
inf * -2 == -inf;
\end{urbiassert}


\item[isInf]%
  Whether \this is infinite.
\begin{urbiassert}
    !0.isInf; !1.isInf; !(-1).isInf;
  !Math.nan.isInf;
   inf.isInf;  (-inf).isInf;
\end{urbiassert}


\item[isNan]%
  Whether is NaN.
\begin{urbiassert}
     !0.isNan; !1.isNan; !(-1).isNan;
   !inf.isNan;  !(-inf).isNan;
    nan.isNan;
\end{urbiassert}


\item[limits]
  See \refObject{Float.limits}.


\item[log]%
  The logarithm of the target.
\begin{urbiassert}
0.log == -inf;
1.log == 0;
1.exp.log == 1;
\end{urbiassert}


\item[max](<arg1>, ...)%
  Bounces to \refSlot[List]{max} on \lstinline|[this, \var{arg1}, ...]|.
\begin{urbiassert}
1.max == 1;
1.max(2, 3) == 3;
3.max(1, 2) == 3;
\end{urbiassert}


\item[min](<arg1>, ...)%
  Bounces to \refSlot[List]{min} on \lstinline|[this, \var{arg1}, ...]|.
\begin{urbiassert}
1.min == 1;
1.min(2, 3) == 1;
3.min(1, 2) == 1;
\end{urbiassert}


\item[nan]%
  The ``not a number'' special float value.  More precisely, this
  returns the ``quiet NaN'', i.e., it is propagated in the various
  computations, it does not raise exceptions.
\begin{urbiscript}
Float.nan;
[00000000] nan
inf / inf;
[00000000] nan
\end{urbiscript}

  A NaN has one distinctive property over the other Floats: it is equal to
  no other float, not even itself.  This behavior is mandated by the
  \wref[IEEE_754-2008]{IEEE 754-2008} standard.
\begin{urbiassert}
!(nan === nan); !(nan == nan); nan != nan;
var n = Float.nan;
n === n;  n != nan;
\end{urbiassert}

  Use \refSlot{isNan} to check if a Float is NaN.
\begin{urbiassert}
(inf/inf).isNan;   (inf/-inf).isNan;
(nan / nan).isNan;
(0 * nan).isNan;   (0 * nan).isNan;
\end{urbiassert}


\item[pi]
  $\pi$.
\begin{urbiassert}
Float.pi.cos ** 2 + Float.pi.sin ** 2 == 1;
\end{urbiassert}


\item[random]
  A random integer between 0 (included) and the target (excluded).
\begin{urbiscript}
20.map(function (dummy) { 5.random });
[00000000] [1, 2, 1, 3, 2, 3, 2, 2, 4, 4, 4, 1, 0, 0, 0, 3, 2, 4, 3, 2]
\end{urbiscript}


\item[round] The integral value nearest to \this rounding half-way cases
  away from zero.  See also \refSlot{ceil}, \refSlot{floor}, and
  \refSlot{trunc}.
\begin{urbiassert}
     0.round ==  0;
   1.4.round ==  1;     1.5.round ==  2;    1.6.round ==  2;
(-1.4).round == -1;  (-1.5).round == -2; (-1.6).round == -2;
   inf.round == inf; (-inf).round == -inf;
   nan.round.isNan;
\end{urbiassert}


\item[seq]%
  The sequence of integers from 0 to \this - 1 as a list.  \this must be
  non-negative.
\begin{urbiassert}
0.seq == [];  3.seq == [0, 1, 2];

(-1).seq;
[00004586:error] !!! seq: expected non-negative integer: -1
\end{urbiassert}


\item[sign]
  Return 1 if \this is positive, 0 if it is null, -1
  otherwise.
\begin{urbiassert}
(-1664).sign == -1;    0.sign ==  0;   1664.sign ==  1;
\end{urbiassert}


\item[sin]
  The sine of the target.
\begin{urbiassert}
0.sin == 0;
\end{urbiassert}


\item[sqr]
  Square of the target.
\begin{urbiassert}
1.5.sqr == 2.25 == 1.5 ** 2;
 32.sqr == 1024 ==  32 ** 2;
\end{urbiassert}


\item[sqrt]
  The square root of the target.
\begin{urbiassert}
1024.sqrt == 32 == 1024 ** 0.5;
\end{urbiassert}


\item[srandom]
  Initialized the seed used by the random function.  As opposed to common
  usage, you should not use
\begin{urbiunchecked}
{
  var now = Date.now.timestamp;
  now.srandom;
  var list1 = 20.map(function (dummy) { 5.random });
  now.srandom;
  var list2 = 20.map(function (dummy) { 5.random });
  assert
  {
    list1 == list2;
  }
};
\end{urbiunchecked}


\item[tan]%
  Tangent of \this.
\begin{urbiassert}
0.tan == 0;
(Float.pi/4).tan ~= 1;
\end{urbiassert}


\item[times](<fun>)%
  Call the functional argument \var{fun} \this times.

\begin{urbiscript}
3.times(function () { echo("ping") });
[00000000] *** ping
[00000000] *** ping
[00000000] *** ping
\end{urbiscript}


\item[trunc]%
  The integral value nearest to but no larger in magnitude than \this.  See
  also \refSlot{ceil}, \refSlot{floor}, \refSlot{round}.
\begin{urbiassert}
     0.trunc ==  0;
   1.4.trunc ==  1;     1.5.trunc ==  1;    1.6.trunc ==  1;
(-1.4).trunc == -1;  (-1.5).trunc == -1; (-1.6).trunc == -1;
   inf.trunc == inf; (-inf).trunc == -inf;
   nan.trunc.isNan;
\end{urbiassert}


\item \lstinline='each|'(\var{fun})=\\%% FIXME: Xindy dies: ['each|'](<fun>)%
  Call the functional argument \var{fun} on every integer from 0 to
  target - 1, with tight sequentiality.  The number must be
  non-negative.
\begin{urbiassert}
{
  var res = [];
  3.'each|'(function (i) { res << 100 + i });
  res
}
== [100, 101, 102];

{
  var res = [];
  for|(var x : 3) { res << x; sleep(20ms); res << (100 + x); };
  res
}
== [0, 100, 1, 101, 2, 102];
\end{urbiassert}%>>>>>>
\end{urbiscriptapi}

%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: "../urbi-sdk"
%%% ispell-dictionary: "american"
%%% ispell-personal-dictionary: "../urbi.dict"
%%% fill-column: 76
%%% End:
