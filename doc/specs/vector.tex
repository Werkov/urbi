%% Copyright (C) 2011, Gostai S.A.S.
%%
%% This software is provided "as is" without warranty of any kind,
%% either expressed or implied, including but not limited to the
%% implied warranties of fitness for a particular purpose.
%%
%% See the LICENSE file for more information.

\section{Vector}

\subsection{Prototypes}
\begin{refObjects}
\item[Object]
\end{refObjects}

\subsection{Construction}

The Vector constructor can be either be given a single List (of Floats), or
any number of Floats.

Vector can be constructed by using either Vector.new, or the literal syntax
\lstinline|< >|. However only a subset of the possible expressions are
acceted inside this syntax: boolean operators are not allowed.

\begin{urbiscript}
Vector;
[00000140] <>

Vector.new();
[00000146] <>

Vector.new(1.1);
[00000147] <1.1>

Vector.new(1.1, 2.2, 3.3);
[00000155] <1.1, 2.2, 3.3>

Vector.new("123");
[00000174:error] !!! unexpected "123", expected a Float

Vector.new([]);
[00000187] <>

Vector.new([1, 2, 3, 4, 5]);
[00000189] <1, 2, 3, 4, 5>

Vector.new(["123"]);
[00000193:error] !!! unexpected "123", expected a Float

<1, 1>;
[00000147] <1, 1>

var x = 1|;
<1+1, x+2>;
[00000000] <2, 3>
\end{urbiscript}

\subsection{Slots}

\begin{urbiscriptapi}
\item['*'](<that>)%
  A Vector whose members are member-wise products of \this and \that.  The
  argument \that must either be a Vector of same dimension, or a Float.
\begin{urbiassert}
Vector              * Vector              == Vector;
Vector.new(1, 2, 3) * Vector.new(5, 6, 7) == Vector.new(5, 12, 21);
Vector.new(1, 2, 3) * 3                   == Vector.new(3,  6,  9);

Vector.new(1, 2) * Vector.new(0);
[00000601:error] !!! *: incompatible vector sizes: 2, 1
\end{urbiassert}


\item['+'](<that>)%
  A Vector whose members are member-wise sums of \this and \that.  The
  argument \that must either be a Vector of same dimension, or a Float.
\begin{urbiassert}
Vector              + Vector              == Vector;
<1, 2, 3> + <5, 6, 7> == <6, 8, 10>;
<1, 2, 3> + 3                   == <4, 5,  6>;

<1, 2> + <0>;
[00000601:error] !!! +: incompatible vector sizes: 2, 1
\end{urbiassert}


\item['-'](<that>)%
  A Vector whose members are member-wise subtractions of \this and \that.
  The argument \that must either be a Vector of same dimension, or a Float.
\begin{urbiassert}
Vector              - Vector              == Vector;
<1, 2, 3> - <5, 6, 7> == <-4, -4, -4>;
<1, 2, 3> - 3                   == <-2, -1,  0>;

Vector.new(1, 2) - Vector.new(0);
[00000601:error] !!! -: incompatible vector sizes: 2, 1
\end{urbiassert}


\item['/'](<that>)%
  A Vector whose members are member-wise divisions of \this and \that.  The
  argument \that must either be a Vector of same dimension, or a Float.
\begin{urbiassert}
Vector              / Vector              == Vector;
<6, 4, 1> / <3, 2, 1> == <2, 2, 1>;
<9, 6, 3> / 3                   == <3, 2, 1>;

<1, 2> / <1>;
[00000601:error] !!! /: incompatible vector sizes: 2, 1
\end{urbiassert}


\item['<'](<that>)%
  Whether \this is less than the \that Vector.  This is the lexicographic
  comparison: \this is ``less than'' \that if, from left to right, one of
  its member is ``less than'' the corresponding member of \that:

\begin{urbiassert}
   <0, 0, 0> < <0, 0, 1>;
 !(<0, 0, 1> < <0, 0, 0>);

   <0, 1, 2> < <0, 2, 1>;
 !(<0, 2, 1> < <0, 1, 2>);

   <1, 1, 1> < <2>;
 !(<2>       < <1, 1, 1>);

 !(<0, 1, 2> < <0, 1, 2>);
\end{urbiassert}

  \noindent
  or \that is a prefix (strict) of \this:

\begin{urbiassert}
           <> < <0>;
  !(      <0> < <>);

       <0, 1> < <0, 1, 2>;
  !(<0, 1, 2> < <0, 1>);

  !(<0, 1, 2> < <0, 1, 2>);
\end{urbiassert}

Since Vector derives from \refObject{Orderable}, the other order-based
operators are defined.

\begin{urbiassert}
        <> <= <>;
        <> <= <0, 1, 2>;
 <0, 1, 2> <= <0, 1, 2>;

        <> >= <>;
 <0, 1, 2> >= <>;
 <0, 1, 2> >= <0, 1, 2>;
 <0, 1, 2> >= <0, 0, 2>;

       !(<> > <>);
  <0, 1, 2> > <>;
!(<0, 1, 2> > <0, 1, 2>);
  <0, 1, 2> > <0, 0, 2>;
\end{urbiassert}


\item['=='](<that>)%
  Whether \this and \that have the same size, and members.
\begin{urbiassert}
   <> == <>;
   <1, 2, 3> == <1, 2, 3>;
 !(<1, 2, 3> == <1, 2>);
 !(<1, 2, 3> == <3, 2, 1>);
\end{urbiassert}


\item|'[]'|(<index>)%
  The value stored at \var{index}.
\begin{urbiassert}
var v = <1, 2, 3>;

v[ 0] == 1; v[ 1] == 2; v[ 2] == 3;
v[-3] == 1; v[-2] == 2; v[-1] == 3;

v[4];
[00000100:error] !!! []: invalid index: 4
\end{urbiassert}


\item|'[]='|(<index>, <value>)%
  Set the value stored at \var{index} to \var{value} and return it.
\begin{urbiassert}
var v = <0, 0, 0>;

(v[ 0] = 1) == 1; (v[ 1] = 2) == 2; (v[ 2] = 3) == 3;
 v[ 0]      == 1;  v[ 1]      == 2;  v[ 2]      == 3;

(v[-3] = 4) == 4; (v[-2] = 5) == 5; (v[-1] = 6) == 6;
 v[-3]      == 4;  v[-2]      == 5;  v[-1]      == 6;

v[4] = 7;
[00000100:error] !!! []=: invalid index: 4
\end{urbiassert}


\item[asString]%
\begin{urbiassert}
  <>.asString() == "<>";
  <1, 2>.asString() == "<1, 2>";
  <1, 2, 3.4>.asString() == "<1, 2, 3.4>";

\end{urbiassert}

\item[asList]%
  Return this as a list.
\begin{urbiassert}
  <>.asList() == [];
  <1,-1>.asList() == [1, -1];
\end{urbiassert}

\item[asVector]%
  Return \this.
\begin{urbiassert}
var v = Vector.new();
v.asVector() === v;
\end{urbiassert}


\item[combAdd](<that>)%
  A \refObject{Matrix} \var{res} such that
  \lstinline|\var{res}[\var{i}, \var{j}] = this[i] + \var{that}[j]|, for all
  \lstinline|i| in the range of \this, and \lstinline|j| in the range of
  \that.
\begin{urbiassert}
<0, 1, 2>.combAdd(<0, 10, 20>)
  == Matrix.new([ 0, 10, 20],
                [ 1, 11, 21],
                [ 2, 12, 22]);
\end{urbiassert}


\item[combDiv](<that>)%
  A \refObject{Matrix} \var{res} such that
  \lstinline|\var{res}[\var{i}, \var{j}] = this[i] / \var{that}[j]|, for all
  \lstinline|i| in the range of \this, and \lstinline|j| in the range of
  \that.
\begin{urbiassert}
<10, 20>.combDiv(<2, 5, 10>)
  == Matrix.new([  5, 2, 1],
                [ 10, 4, 2]);
\end{urbiassert}


\item[combMul](<that>)%
  A \refObject{Matrix} \var{res} such that
  \lstinline|\var{res}[\var{i}, \var{j}] = this[i] * \var{that}[j]|, for all
  \lstinline|i| in the range of \this, and \lstinline|j| in the range of
  \that.
\begin{urbiassert}
<1, 2, 3, 4>.combMul(<5, 6, 7>)
  == Matrix.new([ 5,  6,  7],
                [10, 12, 14],
                [15, 18, 21],
                [20, 24, 28]);
\end{urbiassert}


\item[combSub](<that>)%
  A \refObject{Matrix} \var{res} such that
  \lstinline|\var{res}[\var{i}, \var{j}] = this[i] - \var{that}[j]|, for all
  \lstinline|i| in the range of \this, and \lstinline|j| in the range of
  \that.
\begin{urbiassert}
<-1, 0, +1>.combSub(<-2, 0, +2>)
  == Matrix.new([1, -1, -3],
                [2,  0, -2],
                [3,  1, -1]);
\end{urbiassert}


\item[distance](<that>)%
  The (Euclidean) distance between \this and \that: (Euclidean) norm of
  \lstinline|this - \var{that}|.  They must have equal dimensions.
\begin{urbiassert}
  <>.distance(<>) == 0;
  <0, 0>.distance(<3, 4>) == 5;
  <3, 4>.distance(<0, 0>) == 5;
\end{urbiassert}

\begin{urbiscript}
<0>.distance(<1, 2>);
[00000319:error] !!! distance: incompatible vector sizes: 1, 2

<1, 2>.distance(<0>);
[00000334:error] !!! distance: incompatible vector sizes: 2, 1
\end{urbiscript}


\item[norm]%
  The (Euclidean) norm of \this: square root of the sum of the square of the
  members.
\begin{urbiassert}
 <>.norm == 0;
 <0>.norm == 0;
 <1>.norm == 1;
 <1, 1>.norm == 2.sqrt();
 <0, 0, 0, 0>.norm == 0;
 <1, 1, 1, 1>.norm == 2;
\end{urbiassert}

\item[resize](<dim>)%
  Change the dimensions of \this, using 0 for possibly new members.  Return
  \this.
\begin{urbiscript}
// Check that <1, 2> is equal to the Vector composed of values,
// when resized the number of values.
function resized(var values[])
{
  var m = <1, 2>;
  var res = Vector.new(values);
  // Resize returns this...
  m.resize(res.size) === m;
  // ...and does resize.
  m == res;
}|;

assert
{
  resized();
  resized(1);
  resized(1, 2);
  resized(1, 2, 0);
};
\end{urbiscript}


\item[scalarEQ](<that>)%
  A Vector whose members are 0 or 1 depending on the equality of the
  corresponding members of \this and \that.
\begin{urbiassert}
  <1, 2, 3>.scalarEQ(<3, 2, 1>) == <0, 1, 0>;
\end{urbiassert}
  \this and \that must have equal sizes.
\begin{urbiscript}
<1, 2>.scalarEQ(<0>);
[00000601:error] !!! scalarEQ: incompatible vector sizes: 2, 1
\end{urbiscript}


\item[scalarGE](<that>)%
  A Vector whose members are 0 or 1 if the corresponding member of \this is
  greater than or equal to the one of \that.
\begin{urbiassert}
  <1, 2, 3>.scalarGE(<3, 2, 1>) == <0, 1, 1>;
\end{urbiassert}

  \this and \that must have equal sizes.
\begin{urbiscript}
<1, 2>.scalarGE(<0>);
[00000601:error] !!! scalarGE: incompatible vector sizes: 2, 1
\end{urbiscript}


\item[scalarGT](<that>)%
  A Vector whose members are 0 or 1 if the corresponding member of \this is
  greater than to the one of \that.
\begin{urbiassert}
  <1, 2, 3>.scalarGT(<3, 2, 1>) == <0, 0, 1>;
\end{urbiassert}

  \this and \that must have equal sizes.
\begin{urbiscript}
<1, 2>.scalarGT(<0>);
[00000601:error] !!! scalarGT: incompatible vector sizes: 2, 1
\end{urbiscript}


\item[scalarLE](<that>)%
  A Vector whose members are 0 or 1 if the corresponding member of \this is
  less than or equal to the one of \that.
\begin{urbiassert}
  <1, 2, 3>.scalarLE(<3, 2, 1>) == <1, 1, 0>;
\end{urbiassert}

  \this and \that must have equal sizes.
\begin{urbiscript}
<1, 2>.scalarLE(<0>);
[00000601:error] !!! scalarLE: incompatible vector sizes: 2, 1
\end{urbiscript}


\item[scalarLT](<that>)%
  A Vector whose members are 0 or 1 if the corresponding member of \this is
  less than to the one of \that.
\begin{urbiassert}
  <1, 2, 3>.scalarLT(<3, 2, 1>) == <1, 0, 0>;
\end{urbiassert}

  \this and \that must have equal sizes.
\begin{urbiscript}
<1, 2>.scalarLT(<0>);
[00000601:error] !!! scalarLT: incompatible vector sizes: 2, 1
\end{urbiscript}


\item[set](<list>)%
  Change the members of \this, and return \this.
\begin{urbiassert}
var v = <1, 2, 3>;
v.set([10, 20]) === v;
v == <10, 20>;
\end{urbiassert}


\item[size]%
  The size of the vector.
\begin{urbiassert}
                      <>.size == 0;
                     <0>.size == 1;
Vector.new(0, 1, 2, 3)  .size == 4;
Vector.new([0, 1, 2, 3]).size == 4;
\end{urbiassert}


\item[sum]%
  The sum of the values stored in \this.
\begin{urbiassert}
<>            .sum() == 0;
<1>           .sum() == 1;
<0, 1, 2, 3>  .sum() == 6;
\end{urbiassert}


\item[trueIndexes]%
  A Vector whose values are the indexes of \this whose value is non-null.
\begin{urbiassert}
<>.trueIndexes() == <>;
<1, 0, 1, 0, 0, 1>.trueIndexes() == <0, 2, 5>;
\end{urbiassert}

\item[zip](<others>)%
  Zip this vector with the given list of other vectors, which can be of different sizes.

\begin{urbiassert}
 <0,3>.zip([<1, 4>, <2, 5>]) == <0, 1, 2, 3, 4, 5>
\end{urbiassert}

\item[serialize](<wordSize>, <littleEndian>)%
  Serialize to binary integral data.

\begin{urbiassert}
  <1, 2>.serialize(2, false) == "\x01\x00\x02\x00";
  <1, 2>.serialize(2, true)  == "\x00\x01\x00\x02";
\end{urbiassert}

\item[type]%
  \lstinline|"Vector"|.
\begin{urbiassert}
Vector.type == "Vector";
<>.type == "Vector";
\end{urbiassert}
\end{urbiscriptapi}

%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: "../urbi-sdk"
%%% ispell-dictionary: "american"
%%% ispell-personal-dictionary: "../urbi.dict"
%%% fill-column: 76
%%% End:
