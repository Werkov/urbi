\section{Dictionary}

A \dfn{dictionary} is an \dfn{associative array}, also known as a
\dfn{hash} in some programming languages.  They are arrays whose
indexes are strings.

In a way objects are dictionaries: one can use \lstinline|setSlot|,
\lstinline|updateSlot|, and \lstinline|getSlot|.  This is unsafe since
slots also contains value and methods that object depend upon to run
properly.

\subsection{Example}

The following session demonstrates the features of the Dictionary
objects.

\begin{urbiscript}
var d = Dictionary.new("one", 1, "two", 2);
[00000001] Dictionary {"one" => 1, "two" => 2}
for (var p : d)
  echo (p.first + " -> " + p.second);
[00000003] *** one -> 1
[00000002] *** two -> 2
"three" in d;
[00000004] false
d["three"] = d["one"] + d["two"] | {};
"three" in d;
[00000005] true
d.getWithDefault("four", 4);
[00000006] 4
\end{urbiscript}


\subsection{Prototypes}

\begin{itemize}
\item \refObject{Object}
\end{itemize}

\subsection{Construction}

Dictionnary are created like any other object. The constructor can
take couples of argument to store them in the dictionary.

\begin{urbiscript}[firstnumber=last]
Dictionary.new("one", 1, "two", 2);
[00000000] Dictionary {"one" => 1, "two" => 2}
\end{urbiscript}

\subsection{Methods}

\begin{itemize}
\item \lstinline|asBool|\\
  Negation of \lstinline|Dictionary.empty|.
\begin{urbiscript}[firstnumber=last]
assert(Dictionary.new.asBool == false);
assert(Dictionary.new.set("key", "value").asBool == true);
\end{urbiscript}

\item \lstinline|asList|\\
  Return the contents of the dictionary as a \refObject{Pair} list
  (\var{key}, \var{value}).  This makes it easier to iterate over a
  Dictionary using \lstinline|for|.  No particular order is ensured.

\begin{urbiscript}[firstnumber=last]
Dictionary.new("one", 1, "two", 2).asList;
[00000000] [("one", 1), ("two", 2)]
\end{urbiscript}

\item \lstinline|clear|\\
  Empty the dictionary.

\begin{urbiscript}[firstnumber=last]
assert(Dictionary.new("one", 1).clear.empty());
\end{urbiscript}

\item \lstinline|empty|\\
  Whether the dictionary is empty.

\begin{urbiscript}[firstnumber=last]
assert(Dictionary.new.empty);
assert(!Dictionary.new.set("key", "value").empty);
\end{urbiscript}

\item \lstinline|erase(\var{key})|\\
  Remove the mapping for \lstinline|\var{key}|.

\begin{urbiscript}[firstnumber=last]
Dictionary.new("one", 1, "two", 2).erase("two");
[00386250] Dictionary {"one" => 1}
\end{urbiscript}

\item \lstinline|get(\var{key})|\\
  Return the value associated to  \lstinline|\var{key}| if it exists,
  \lstinline|void| otherwise.
\begin{urbiscript}[firstnumber=last]
assert(Dictionary.new("one", 1, "two", 2).get("one") == 1);
assert(Dictionary.new("one", 1, "two", 2).get("three").isVoid);
\end{urbiscript}


\item \lstinline|getWithDefault(\var{key}, \var{default-value})|\\
  Return the value associated to  \lstinline|\var{key}| if it exists,
  \lstinline|\var{default-value}| otherwise.

\begin{urbiscript}[firstnumber=last]
do (Dictionary.new("one", 1, "two", 2))
{
  assert(getWithDefault("one",  -1) == 1);
  assert(getWithDefault("three", 3) == 3);
}|;
\end{urbiscript}


\item \lstinline|has(\var{key})|\\
  Whether the dictionary has a mapping for \lstinline|\var{key}|.

\begin{urbiscript}[firstnumber=last]
do (Dictionary.new("one", 1))
{
  assert(has("one"));
  assert(!has("zero"));
}|;
\end{urbiscript}

\item \lstinline|init(\var{key1}, \var{value1}, ...)|~\\
  Insert the mapping from \lstinline|\var{key1}| to
  \lstinline|\var{value1}| and so forth.

\begin{urbiscript}[firstnumber=last]
Dictionary.clone.init("one", 1, "two", 2);
[00000000] Dictionary {"one" => 1, "two" => 2}
\end{urbiscript}

\item \lstinline|keys|\\
  The list of all the keys.  No particular order is ensured.
\begin{urbiscript}[firstnumber=last]
assert(Dictionary.new("one", 1, "two", 2).keys == ["one", "two"]);
\end{urbiscript}

\item \lstinline|set(\var{key}, \var{value})|\\
  Map \lstinline|\var{key}| to \lstinline|\var{value}| and return
  \lstinline|this| so that invocations to \lstinline|set| can be
  chained.  The possibly existing previous mapping is overriden.

\begin{urbiscript}[firstnumber=last]
Dictionary.new.set("one", 2).set("one", 1);
[00000000] Dictionary {"one" => 1}
\end{urbiscript}

\item \lstinline|[]=(\var{key}, \var{value})|\\
  Syntactic sugar for \lstinline|set(\var{key}, \var{value})|, but
  returns \var{value}.

\begin{urbiscript}[firstnumber=last]
{
  var d = Dictionary.new("one", "2");
  assert((d["one"] = 1) == 1);
  assert(d["one"] == 1);
};
\end{urbiscript}

\item \lstinline|[](\var{key})|\\
  Syntactic sugar for \lstinline|get(\var{key})|.

\begin{urbiscript}[firstnumber=last]
assert(Dictionary.new("one", 1)["one"] == 1);
\end{urbiscript}

\end{itemize}


%%% Local Variables:
%%% mode: latex
%%% TeX-master: "../urbi-sdk"
%%% End:
