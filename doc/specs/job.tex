%% Copyright (C) 2010, 2011, Gostai S.A.S.
%%
%% This software is provided "as is" without warranty of any kind,
%% either expressed or implied, including but not limited to the
%% implied warranties of fitness for a particular purpose.
%%
%% See the LICENSE file for more information.

\section{Job}

Jobs are independent threads of executions.  Jobs can run concurrently.
They can also be managed using \refObject[s]{Tag}.

\subsection{Prototypes}

\begin{refObjects}
\item[Object]
\item[Traceable]
\end{refObjects}

\subsection{Construction}

A Job is typically constructed via \refSlot[Control]{detach},
\refSlot[Control]{disown}, or \refSlot[System]{spawn}.

\begin{urbiscript}
detach(sleep(10));
[00202654] Job<shell_4>

disown(sleep(10));
[00204195] Job<shell_5>

spawn (function () { sleep(10) }, false);
[00274160] Job<shell_6>
\end{urbiscript}

\subsection{Slots}

\begin{urbiscriptapi}
\item[asJob]
  Return \lstinline|this|.
\begin{urbiassert}
Job.current.asJob === Job.current;
\end{urbiassert}


\item[asString] The string \lstinline|Job<\var{name}>| where \var{name} is
  the name of the job.
\begin{urbiassert}
Job.current.asString == "Job<shell>";
spawn (function () { sleep(10) }, false).asString == "Job<shell_11>";
\end{urbiassert}

\item[backtrace] The current backtrace of the job as a list of
  \refObject[s]{StackFrame}.  Uses \refSlot[Traceable]{backtrace}.

\begin{urbiscript}
//#push 1 "file.u"
var s = detach(sleep(10))|;
// Leave some time for s to be started.
sleep(1);
assert
{
  s.backtrace[0].asString == "file.u:1.16-24: sleep";
  s.backtrace[1].asString == "file.u:1.16-24: detach";
};
//#pop
\end{urbiscript}

  In the case of events, the backtrace is composite: the bottom part
  corresponds to the location of the event handler, and the top part is the
  location of the event emission.  The event emission is denoted by
  \refSlot[Event]{emit} rather than \lstinline|!|, and its reception by
  \refSlot[Event]{onEvent} rather than \lstinline|?| or \lstinline|at|.

\begin{urbiscript}
//#push 1 "file.u"
var e = Event.new|;
at (e?)
  lobby.echoEach(Job.current.backtrace);
e!;
[00000149] *** file.u:3.18-38: backtrace
[00000149] *** file.u:4.1: emit
[00000149] *** ---- event handler backtrace:
[00000149] *** file.u:2.1-3.41: onEvent

//#push 1 "file.u"
var f = Event.new|;
function watchEvent()
{
  at (f?)
    lobby.echoEach(Job.current.backtrace);
}|;
function sendEvent()
{
  f!;
}|;
watchEvent;
sendEvent;
[00008208] *** file.u:5.20-40: backtrace
[00008208] *** file.u:9.3: emit
[00008208] *** file.u:12.1-9: sendEvent
[00008208] *** ---- event handler backtrace:
[00008208] *** file.u:4.3-5.43: onEvent
[00008208] *** file.u:11.1-10: watchEvent
\end{urbiscript}

\item[clone]
  Cloning a job is forbidden.

\item[current]%
  The \refObject{Job} in charge of executing this ``thread'' of code.
\begin{urbiscript}
var j = Job.current|;
assert { j.isA(Job) };

var j1; var j2;

// Both sequential compositions use the same job: the current one.
{ j1 = Job.current; j2 = Job.current }|;
assert { j1 == j2; j1 == j };

{ j1 = Job.current | j2 = Job.current }|;
assert { j1 == j2; j1 == j };

// Concurrency requires several jobs: j1 and j2 are different.
//
// As an optimization, the current job is used for one of these
// jobs.  This is an implementation detail, do not rely on it.
{ j1 = Job.current & j2 = Job.current }|;
assert { j1 != j2; j1 == j; j2 != j };

{ j1 = Job.current, j2 = Job.current }|;
assert { j1 != j2; j1 != j; j2 == j };
\end{urbiscript}

\item[dumpState]
  Pretty-print the state of the job.

\begin{urbiscript}
//#push 1 "file.u"
var t = detach(sleep(10))|;
// Leave some time for s to be started.
sleep(1);
t.dumpState;
[00004295] *** Job: shell_10
[00004295] ***   State: sleeping
[00004295] ***   Tags:
[00004295] ***     Tag<Lobby_1>
[00004297] ***   Backtrace:
[00004297] ***     file.u:1.16-24: sleep
[00004297] ***     file.u:1.16-24: detach
//#pop
\end{urbiscript}

\item[name]  The name of the job.
\begin{urbiscript}
detach(sleep(10)).name;
[00004297] "shell_5"
\end{urbiscript}

\item[resetStats]%
  Reinitialize the \refSlot{stats} computation.

\item[status] A \refObject{String} that describes the current status of the
  job (starting, running, \ldots), and its properties (frozen, \ldots).
\begin{urbiassert}
var t = detach(sleep(10));
Job.current.status == "running (current job)";
t.status == "sleeping" ;
\end{urbiassert}

\item[stats]%
  Return a \refObject{Dictionary} containing information about the execution
  cycles of \urbi.  This is an internal feature made for developers, it
  might be changed without notice.  See also \refSlot{resetStats}.
\begin{urbicomment}
removeSlots("j");
\end{urbicomment}
\begin{urbiscript}
var j = detach({
  // Each ';' increment the Cycles with their execution time.
  var i = 0;
  {
    // Increment the Waiting time.
    waituntil(i == 1);
    // Increment the Sleeping time.
    sleep(100ms);
    i = 2;
  }, // Will fork and join a child job.

  sleep(200ms);
  i = 1;
  waituntil(i != 1);

  // Stop breaks the workflow of each job tagged with the tag.
  var t = Tag.new;
  t: t.stop(21);
})|;
j.waitForTermination;
var stats = j.stats|;

Float.epsilonTilde = 0.01 |;
assert
{
  0 < stats["Cycles"];
  stats["CyclesMin"] <= stats["CyclesMean"] <= stats["CyclesMax"];
  stats["WaitingMin"] <= stats["WaitingMean"] <= stats["WaitingMax"] ~= 100ms;
  stats["SleepingMin"] <= stats["SleepingMean"] <= stats["SleepingMax"] ~= 200ms;
  stats["Fork"] == stats["Join"] == 1;
  stats["WorkflowBreak"] == 1;

  stats["TerminatedChildrenWaitingMean"] ~= 200ms;
  stats["TerminatedChildrenSleepingMean"] ~= 100ms;
};
\end{urbiscript}


\item[tags] The list of \refObject[s]{Tag} that manage this job.

\item[terminate]  Kill this job.
\begin{urbiscript}
var r = detach({ sleep(1s); echo("done") })|;
assert (r in jobs);
r.terminate;
assert (r not in jobs);
sleep(2s);
\end{urbiscript}

\item[timeShift]
  Get the total amount of time during which we were frozen.
\begin{urbiscript}
tag: r = detach({ sleep(3); echo("done") })|;
tag.freeze();
sleep(2);
tag.unfreeze();
Math.round(r.timeShift);
[00000001] 2
\end{urbiscript}

\item[waitForChanges] Resume the scheduler, putting the current Job in a
  waiting status.  The scheduler may reschedule the job immediately.

\item[waitForTermination] Wait for the job to terminate before resuming
  execution of the current one.  If the job has already terminated, return
  immediately.
\end{urbiscriptapi}


%%% Local Variables:
%%% mode: latex
%%% TeX-master: "../urbi-sdk"
%%% ispell-dictionary: "american"
%%% ispell-personal-dictionary: "../urbi.dict"
%%% fill-column: 76
%%% End:
