%% Copyright (C) 2010, 2011, Gostai S.A.S.
%%
%% This software is provided "as is" without warranty of any kind,
%% either expressed or implied, including but not limited to the
%% implied warranties of fitness for a particular purpose.
%%
%% See the LICENSE file for more information.

\section{Job}

Jobs are independent threads of executions.  Jobs can run concurrently.
They can also be managed using \refObject[Tag]{Tags}.

\subsection{Prototypes}

\begin{refObjects}
\item[Object]
\item[Traceable]
\end{refObjects}

\subsection{Construction}

A Job is typically constructed via \refSlot[Control]{detach},
\refSlot[Control]{disown}, or \refSlot[Code]{spawn}.

\begin{urbiscript}
detach(sleep(10));
[00202654] Job<shell_4>

disown(sleep(10));
[00204195] Job<shell_5>

function () { sleep(10) }.spawn(false);
[00274160] Job<shell_6>
\end{urbiscript}

\subsection{Slots}

\begin{urbiscriptapi}
\item[asJob]
  Return \lstinline|this|.
\begin{urbiassert}
Job.current.asJob === Job.current;
\end{urbiassert}


\item[asString] The string \lstinline|Job<\var{name}>| where \var{name} is
  the name of the job.
\begin{urbiassert}
Job.current.asString == "Job<shell>";
var a = function () { sleep(1) }.spawn(false);
a.asString == "Job<" + a.name + ">";
\end{urbiassert}


\item[backtrace] The current backtrace of the job as a \refObject{List} of
  \refObject[StackFrame]{StackFrames} starting from innermost, to outermost.
  Uses \refSlot[Traceable]{backtrace}.

\begin{urbiscript}
//#push 100 "foo.u"
function innermost () { Job.current.backtrace }|;
function inner ()     { innermost }|;
function outer ()     { inner }|;
function outermost () { outer }|;
echoEach(outermost);
[00001732] *** foo.u:100.25-45: backtrace
[00001732] *** foo.u:101.25-33: innermost
[00001732] *** foo.u:102.25-29: inner
[00001732] *** foo.u:103.25-29: outer
[00001732] *** foo.u:104.10-18: outermost
//#pop

//#push 1 "file.u"
var s = detach(sleep(1))|;
// Leave some time for s to be started.
sleep(100ms);
assert
{
  s.backtrace[0].asString == "file.u:1.16-23: sleep";
  s.backtrace[1].asString == "file.u:1.9-24: detach";
};
// Leave some time for sleep to return.
sleep(1);
assert
{
  s.backtrace.size == 1;
  s.backtrace[0].asString == "file.u:1.9-24: detach";
};
//#pop
\end{urbiscript}

  In the case of events, the backtrace is composite: the bottom part
  corresponds to the location of the event handler, and the top part is the
  location of the event emission.  The event emission is denoted by
  \refSlot[Event]{emit} rather than \lstinline|!|, and its reception by
  \refSlot[Event]{onEvent} rather than \lstinline|?| or \lstinline|at|.

\begin{urbiscript}
//#push 1 "file.u"
var e = Event.new|;
function showBacktrace(var where)
{
  echo("==================== " + where)|
  echoEach(Job.current.backtrace);
}|;
at (e?)
  showBacktrace("at-enter")
onleave
  showBacktrace("at-leave");
e!;
[00000647] *** ==================== at-enter
[00000647] *** file.u:5.12-32: backtrace
[00000647] *** file.u:8.3-27: showBacktrace
[00000647] *** file.u:11.1: emit
[00000647] *** ---- event handler backtrace:
[00000647] *** file.u:7.1-10.27: onEvent
[00000647] *** ==================== at-leave
[00000647] *** file.u:5.12-32: backtrace
[00000647] *** file.u:10.3-27: showBacktrace
[00000647] *** file.u:11.1: emit
[00000647] *** ---- event handler backtrace:
[00000647] *** file.u:7.1-10.27: onEvent


//#push 1 "file.u"
var f = Event.new|;
function watchEvent()
{
  at (f?)
    showBacktrace("at-enter")
  onleave
    showBacktrace("at-leave");
}|;
function sendEvent()
{
  f!;
}|;
watchEvent;
sendEvent;
[00000654] *** ==================== at-enter
[00000654] *** file.u:5.12-32: backtrace
[00000654] *** file.u:5.5-29: showBacktrace
[00000654] *** file.u:11.3: emit
[00000654] *** file.u:14.1-9: sendEvent
[00000654] *** ---- event handler backtrace:
[00000654] *** file.u:4.3-7.29: onEvent
[00000654] *** file.u:13.1-10: watchEvent
[00000654] *** ==================== at-leave
[00000654] *** file.u:5.12-32: backtrace
[00000654] *** file.u:7.5-29: showBacktrace
[00000654] *** file.u:11.3: emit
[00000654] *** file.u:14.1-9: sendEvent
[00000654] *** ---- event handler backtrace:
[00000654] *** file.u:4.3-7.29: onEvent
[00000654] *** file.u:13.1-10: watchEvent
\end{urbiscript}


\item[clone]
  Cloning a job is forbidden.


\item[current]%
  The \refObject{Job} in charge of executing this ``thread'' of code.
\begin{urbiscript}
var j = Job.current|;
assert { j.isA(Job) };

var j1; var j2;

// Both sequential compositions use the same job: the current one.
{ j1 = Job.current; j2 = Job.current }|;
assert { j1 == j2; j1 == j };

{ j1 = Job.current | j2 = Job.current }|;
assert { j1 == j2; j1 == j };

// Concurrency requires several jobs: j1 and j2 are different.
//
// As an optimization, the current job is used for one of these
// jobs.  This is an implementation detail, do not rely on it.
{ j1 = Job.current & j2 = Job.current }|;
assert { j1 != j2; j1 == j; j2 != j };

{ j1 = Job.current, j2 = Job.current }|;
assert { j1 != j2; j1 != j; j2 == j };
\end{urbiscript}


\item[dumpState]
  Pretty-print the state of the job.

\begin{urbiscript}
//#push 1 "file.u"
var t = detach(sleep(1))|;
// Leave some time for s to be started.
sleep(100ms);
t.dumpState;
[00004295] *** Job: shell_10
[00004295] ***   State: sleeping
[00004295] ***   Tags:
[00004295] ***     Tag<Lobby_1>
[00004297] ***   Backtrace:
[00004297] ***     file.u:1.16-23: sleep
[00004297] ***     file.u:1.9-24: detach
//#pop
\end{urbiscript}


\item[jobs]%
  All the existing jobs as a \refObject{List}.
\begin{urbiassert}
Job.jobs.isA(List);
Job.current in Job.jobs;
\end{urbiassert}


\item[name] The name of the job as a \refObject{String}.
\begin{urbiscript}
Job.current.name;
[00004293] "shell"
detach(sleep(1)).name;
[00004297] "shell_11"
\end{urbiscript}


\item[resetStats]%
  Reinitialize the \refSlot{stats} computation.

\item[tags] The list of \refObject[Tag]{Tags} that manage this job.

\item[stats]%
  Return a \refObject{Dictionary} containing information about the execution
  cycles of \urbi.  This is an internal feature made for developers, it
  might be changed without notice.  See also \refSlot{resetStats}.
\begin{urbicomment}
removeSlots("j");
\end{urbicomment}
\begin{urbiscript}
var j = detach({
  // Each ';' increment the Cycles with their execution time.
  var i = 0;
  {
    // Increment the Waiting time.
    waituntil(i == 1);
    // Increment the Sleeping time.
    sleep(100ms);
    i = 2;
  }, // Will fork and join a child job.

  sleep(200ms);
  i = 1;
  waituntil(i != 1);

  // Stop breaks the workflow of each job tagged with the tag.
  var t = Tag.new;
  t: t.stop(21);
})|;
j.waitForTermination;
var stats = j.stats|;

Float.epsilonTilde = 0.01 |;
assert
{
  0 < stats["Cycles"];
  stats["CyclesMin"] <= stats["CyclesMean"] <= stats["CyclesMax"];
  stats["WaitingMin"] <= stats["WaitingMean"] <= stats["WaitingMax"] ~= 100ms;
  stats["SleepingMin"] <= stats["SleepingMean"] <= stats["SleepingMax"] ~= 200ms;
  stats["Fork"] == stats["Join"] == 1;
  stats["WorkflowBreak"] == 1;

  stats["TerminatedChildrenWaitingMean"] ~= 200ms;
  stats["TerminatedChildrenSleepingMean"] ~= 100ms;
};
\end{urbiscript}


\item[status] A \refObject{String} that describes the current status of the
  job (starting, running, \ldots), and its properties (frozen, \ldots).
\begin{urbiassert}
var t = detach(sleep(10));
Job.current.status == "running (current job)";
t.status == "sleeping" ;
\end{urbiassert}


\item[tags] The list of \refObject[Tag]{Tags} that manage this job.


\item[terminate]  Kill this job.
\begin{urbiscript}
var r = detach({ sleep(1s); echo("done") })|;
assert (r in Job.jobs);
r.terminate;
assert (r not in Job.jobs);
sleep(2s);
\end{urbiscript}


\item[timeShift]
  Get the total amount of time during which we were frozen.
\begin{urbiscript}
tag: r = detach({ sleep(3); echo("done") })|;
tag.freeze();
sleep(2);
tag.unfreeze();
Math.round(r.timeShift);
[00000001] 2
\end{urbiscript}


\item[waitForTermination] Wait for the job to terminate before resuming
  execution of the current one.  If the job has already terminated, return
  immediately.
\end{urbiscriptapi}


%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: "../urbi-sdk"
%%% ispell-dictionary: "american"
%%% ispell-personal-dictionary: "../urbi.dict"
%%% fill-column: 76
%%% End:
