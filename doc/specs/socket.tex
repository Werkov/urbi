%% Copyright (C) 2009-2011, Gostai S.A.S.
%%
%% This software is provided "as is" without warranty of any kind,
%% either expressed or implied, including but not limited to the
%% implied warranties of fitness for a particular purpose.
%%
%% See the LICENSE file for more information.

\section{Socket}

A \dfn{Socket} can manage asynchronous input/output network connections.

\subsection{Example}

The following example demonstrates how both the \refObject{Server} and
\refObject{Socket} object work.

This simple example will establish a dialog between \lstinline|server| and
\lstinline|client|.  The following object, \lstinline|Dialog|, contains the
script of this exchange.  It is put into \refObject{Global} so that both the
server and client can read it.  \lstinline|Dialog.reply(var \var{s})|
returns the reply to a message \var{s}.

\begin{urbiscript}
class Dialog
{
  var lines =
  [
    "Hi!",
    "Hey!",
    "Hey you doin'?",
    "Whazaaa!",
    "See ya.",
  ]|;

  function reply(var s)
  {
    for (var i: lines.size - 1)
      if (s == lines[i])
        return lines[i + 1];
    "off";
  }
}|;
// Import lobby so that Dialog is visible from everywhere.
import this.*;
\end{urbiscript}

The server, an instance of \refObject{Server}, expects incoming connections,
notified by \refSlot[Server]{connection} event.  Once the connection
establish, it listens to the \lstinline|socket| for incoming messages,
notified by the \refSlot{received} event.  Its reaction to this event is to
send the following line of the dialog.  At the end of the dialog, the socket
is disconnected.

\begin{urbiscript}
var server =
  do (Server.new())
  {
    at (connection?(var socket))
      at (socket.received?(var data))
      {
        var reply = Dialog.reply(data);
        socket.write(reply);
        echo("server: " + reply);
        if (reply == "off")
          socket.disconnect();
      };
  }|;
\end{urbiscript}

The client, an instance of \refObject{Socket} expects incoming messages,
notified by the \refSlot{received} event.  Its reaction is to send the
following line of the dialog.

\begin{urbiscript}
var client =
  do (Socket.new())
  {
    at (received?(var data))
    {
      var reply = Dialog.reply(data);
      write(reply);
      echo("client: " + reply);
    };
  }|;
\end{urbiscript}

The server is then activated, listening to incoming connections on a port
that will be chosen by the system among the free ones.

\begin{urbiscript}
server.listen("localhost", "0");
clog << "connecting to %s:%s" % [server.host, server.port];
\end{urbiscript}

The client connects to the server, and initiates the dialog.

\begin{urbiscript}
client.connect(server.host, server.port);
echo("client: " + Dialog.lines[0]);
client.write(Dialog.lines[0]);
[00000003] *** client: Hi!
\end{urbiscript}

Because this dialog is asynchronous, the easiest way to wait for the dialog
to finish is to wait for the \refSlot{disconnected} event.

\begin{urbiscript}
waituntil(client.disconnected?) | echo("done");
[00000004] *** server: Hey!
[00000005] *** client: Hey you doin'?
[00000006] *** server: Whazaaa!
[00000007] *** client: See ya.
[00000008] *** server: off
[00000008] *** done
\end{urbiscript}

There is a catch though: the last message was still being processed by the
system, and arrived \emph{after} we waited for the \lstinline{client} is to
be \lstinline{disconnected}:

\begin{urbiscript}
sleep(100ms);
[00000009] *** client: off
\end{urbiscript}

This is because both the last message sent from the server, and the
disconnection request have arrived at the same ``instant'' to the client.
Both are processed asynchronously, in particular the message reception since
the code used an \emph{asynchronous} \lstinline{at}
(\autoref{sec:lang:at:sync-async}).  In the case of asynchronous event
handling, this is no guarantee on the order of event processing.  This can
be addressed by \emph{synchronous} event processing on the client side; pay
extra attention to the \textkwd{sync} qualifier passed to \lstinline{at}:

\begin{urbiscript}
var syncClient =
  do (Socket.new())
  {
    at sync (received?(var data))
    {
      var reply = Dialog.reply(data);
      write(reply);
      echo("syncClient: " + reply);
    };
  }|;

syncClient.connect(server.host, server.port);
echo("syncClient: " + Dialog.lines[0]);
syncClient.write(Dialog.lines[0]);
[00000003] *** syncClient: Hi!
waituntil(syncClient.disconnected?) | echo("done");
[00000004] *** server: Hey!
[00000005] *** syncClient: Hey you doin'?
[00000006] *** server: Whazaaa!
[00000007] *** syncClient: See ya.
[00000008] *** server: off
[00000008] *** syncClient: off
[00000008] *** done
\end{urbiscript}

\noindent
This time, as one would expect at first, the \lstinline{*** done} appears
after the full dialog was performed.


\subsection{Prototypes}
\begin{refObjects}
\item[Object]
\end{refObjects}

\subsection{Construction}

A \refObject{Socket} is constructed with no argument. At creation, a
new \refObject{Socket} has four own slots: \refSlot{connected},
\refSlot{disconnected}, \refSlot{error} and \refSlot{received}.

\begin{urbiscript}
var s = Socket.new()|;
\end{urbiscript}

\subsection{Slots}
\begin{urbiscriptapi}
\item[connect](<host>, <port>)%
  Connect \this to \var{host} and \var{port}.  The
  \var{port} can be either an integer, or a string that denotes
  symbolic ports, such as \lstinline|"smtp"|, or \lstinline|"ftp"| and
  so forth.


\item[connected]%
  Event launched when the connection is established.


\item[connectSerial](<device>, <baudRate>)%
  Connect \this to the serial port \var{device}, with given
  \var{baudRate}.


\item[disconnect]%
  Close the connection.


\item[disconnected]
  Event launched when a disconnection happens.


\item[error]
  Event launched when an error happens. This event is launched with
  the error message in argument. The event \refSlot{disconnected} is
  also always launched.


\item[getIoService] Return the \refObject{IoService} used by this
  socket. Only the default \refObject{IoService} is automatically polled.


\item[host]
  The remote host of the connection.


\item[isConnected]
  Whether \this is connected.
\begin{urbiassert}
! Socket.new().isConnected;
\end{urbiassert}


\item[localHost]
  The local host of the connection.


\item[localPort]
  The local port of the connection.


\item[poll] Call \lstinline|getIoService.poll()|. This method is called
  regularly every \refSlot{pollInterval} on the \refObject{Socket}
  object. You do not need to call this function on your sockets unless you
  use your own \refObject{IoService}.


\item[pollInterval] Each \refSlot{pollInterval} amount of time,
  \refSlot{poll} is called. If \refSlot{pollInterval} equals zero,
  \refSlot{poll} is not called.


\item[port]
  The remote port of the connection.


\item[received]
  Event launched when \this has received data. The data is
  given by argument to the event.


\item[syncWrite](<data>)%
  Similar to \refSlot{write}, but forces the operation to complete
  synchronously. Synchronous and asynchronous write operations cannot be
  mixed.


\item[write](<data>)%
  Sends \var{data} trough the connection.
\end{urbiscriptapi}

%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: "../urbi-sdk"
%%% ispell-dictionary: "american"
%%% ispell-personal-dictionary: "../urbi.dict"
%%% fill-column: 76
%%% End:
