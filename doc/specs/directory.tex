%% Copyright (C) 2009-2010, Gostai S.A.S.
%%
%% This software is provided "as is" without warranty of any kind,
%% either expressed or implied, including but not limited to the
%% implied warranties of fitness for a particular purpose.
%%
%% See the LICENSE file for more information.

\section{Directory}

A \dfn{Directory} represents a directory of the file system.

\subsection{Prototypes}
\begin{refObjects}
\item[Object]
\end{refObjects}

\subsection{Construction}

A \dfn{Directory} can be constructed with one argument: the path of
the directory using a \refObject{String} or a \refObject{Path}. It can
also be constructed by the method open of \refObject{Path}.

\begin{urbiscript}
Directory.new(".");
[00000001] Directory(".")
Directory.new(Path.new("."));
[00000002] Directory(".")
\end{urbiscript}

\subsection{Slots}
\begin{urbiscriptapi}
\item['/'](<str>)
  The \var{str} \refObject{String} is concatenated with the directory path.
  If the resulting path is either a directory or a file, \refSlot{'/'} will
  returns either a \refObject{Directory} or a \refObject{File} object.
\begin{urbiscript}
var dir1 = Directory.create("dir1")|;
Directory.create("dir1/dir2")|;
File.create("dir1/file")|;
dir1 / "dir2";
[00000001] Directory("dir1/dir2")
dir1 / "file";
[00000002] File("dir1/file")
dir1.removeAll;
\end{urbiscript}

\item['<<'](<entity>)
  Alias for \refSlot[Directory]{copyInto}.
\begin{urbiscript}
dir1 = Directory.create("dir1")|;
var dir2 = Directory.create("dir2")|;
var file = Directory.create("file")|;
dir1 << file << dir2;
[00000001] Directory("dir1")
dir1.content;
[00000003] ["dir2", "file"]
dir1.removeAll;
dir2.removeAll;
\end{urbiscript}

\item[asList]
  The contents of the directory as a \refObject{Path} list.  The
  various paths include the name of the directory \this.

\item[asString] A \refObject{String} containing the path of the directory.
\begin{urbiassert}
Directory.new(".").asString == ".";
\end{urbiassert}

\item[asPath] A \refObject{Path} being the path of the directory.

\item[content]
  The contents of the directory as a \refObject{String} list.  The
  strings include only the last component name; they do not contain
  the directory name of \this.

\item[copy](<dirname>)
  Copy recursively all files and directories in the \lstinline|this| directory
  to the directory \var{dirname} after creating it. Return the new directory.
\begin{urbiscript}
dir1 = Directory.create("dir1")|;
dir2 = Directory.create("dir1/dir2")|;
file = File.create("dir1/file")|;
var directory1 = dir1.copy("directory1");
[00000001] Directory("directory1")
directory1.content;
[00000002] ["dir2", "file"]
dir1.removeAll;
directory1.removeAll;
\end{urbiscript}

\item[copyInto](<entity>)
  If \var{entity} is a \refObject{Directory} or a \refObject{File},
  \refSlot{copyInto} copies \var{entity} in the \lstinline|this| directory.
  Return \lstinline|this| to allow chained operations.
\begin{urbiscript}
dir1 = Directory.create("dir1")|;
dir2 = Directory.create("dir2")|;
file = File.create("file")|;
dir1.copyInto(file).copyInto(dir2);
[00000001] Directory("dir1")
dir1.content;
[00000003] ["dir2", "file"]
dir2;
[00000004] Directory("dir2")
file;
[00000005] File("file")
dir1.removeAll;
dir2.removeAll;
file.remove;
\end{urbiscript}

\item[clear]
  Remove all children recursively but not the directory itself. After a
  call to \refSlot{clear}, a call to \refSlot{empty} should return
  \lstinline|true|.
\begin{urbiscript}
dir1 = Directory.create("dir1")|;
dir2 = Directory.create("dir1/dir2")|;
var file1 = File.create("dir1/file1")|;
var file2 = File.create("dir1/dir2/file2")|;
dir1.content;
[00000001] ["dir2", "file1"]
dir2.content;
[00000002] ["file2"]
dir1.clear;
dir1.content;
[00000003] []
dir1.remove;
\end{urbiscript}

\item[create](<name>)
  Create the directory \var{name} where \var{name} is either a
  \refObject{String} or a \refObject{Path}. In addition to system errors that
  can occur, errors are raised if directory or file \var{name} already exists.
\begin{urbiscript}
var dir = Directory.new("dir");
[00000001:error] !!! new: directory does not exist: "dir"
var dir = Directory.create("dir");
[00000002] Directory("dir")
dir.content;
[00000003] []
dir.remove;
\end{urbiscript}

\item[createAll](<name>)
  Create the directory \var{name} where \var{name} is either a
  \refObject{String} or a \refObject{Path}. If \var{name} is a
  path (or a \refObject{String} describing a path) no errors are
  raised if one directory doesn't exist or already exists. Instead
  \refSlot{createAll} creates them all as in the Unix \samp{make -p} command.
\begin{urbiscript}
Directory.create("dir1/dir2/dir3");
[00000001:error] !!! create: no such file or directory: "dir1/dir2/dir3"
dir1 = Directory.create("dir1");
[00000002] Directory("dir1")
Directory.createAll("dir1/dir2/dir3");
[00000002] Directory("dir1/dir2/dir3")
dir1.removeAll;
\end{urbiscript}

\item[empty]
  Whether the directory is empty.
\begin{urbiscript}
dir = Directory.create("dir")|;
assert(dir.empty);
File.create("dir/file")|;
assert(!dir.empty);
dir.removeAll;
\end{urbiscript}

\item[exists]
  Whether the directory still exists.
\begin{urbiscript}
dir = Directory.create("dir");
[00000001] Directory("dir")
assert(dir.exists);
dir.remove;
assert(!dir.exists);
\end{urbiscript}

\item[fileCreated](<name>)%
  Event launched when a file is created inside the directory.
  May not exist if not supported by your architecture.

%% firstline is used to separate inotify test from the others.
\begin{urbiscript}[firstnumber=1]
if (Path.new("./dummy.txt").exists)
  File.new("./dummy.txt").remove;

  {
    var d = Directory.new(".");
    waituntil(d.fileCreated?(var name));
    assert
    {
      name == "dummy.txt";
      Path.new(d.asString + "/" + name).exists;
    };
  }
&
  {
    sleep(100ms);
    File.create("./dummy.txt");
  }|;
\end{urbiscript}

\item[fileDeleted](<name>)%
  Event launched when a file is deleted from the directory.  May not exist
  if not supported by your architecture.

\begin{urbiscript}
if (!Path.new("./dummy.txt").exists)
  File.create("./dummy.txt")|;

  {
    var d = Directory.new(".");
    waituntil(d.fileDeleted?(var name));
    assert
    {
      name == "dummy.txt";
      !Path.new(d.asString + "/" + name).exists;
    };
  }
&
  {
    sleep(100ms);
    File.new("./dummy.txt").remove;
  }|;
\end{urbiscript}
%% Use firstline after this test if this is not related to inotify.

\item[basename]
  Return a \refObject{String} containing the path of the directory without
  its dirname.
\begin{urbiscript}
var dir1 = Directory.create("dir1");
[00000001] Directory("dir1")
var dir2 = Directory.create("dir1/dir2");
[00000002] Directory("dir1/dir2")
dir1.basename;
[00000002] "dir1"
dir2.basename;
[00000003] "dir2"
dir1.removeAll;
\end{urbiscript}

\item[moveInto](<entity>)
  If \var{entity} is a \refObject{Directory} or a \refObject{File},
  \refSlot{moveInto} moves \var{entity} in the \lstinline|this| directory.
  Return \lstinline|this| to allow chained operations.
\begin{urbiscript}
dir1 = Directory.create("dir1")|;
dir2 = Directory.create("dir2")|;
var file = File.create("file")|;
dir1.moveInto(file).moveInto(dir2);
[00000001] Directory("dir1")
dir1.content;
[00000003] ["dir2", "file"]
dir2;
[00000004] Directory("dir1/dir2")
file;
[00000005] File("dir1/file")
dir1.removeAll;
\end{urbiscript}

\item[parent]
  Return the parent of the directory.
\begin{urbiscript}
Directory.create("dir")|;
var dir = Directory.create("dir/dir")|;
dir.parent;
[00000001] Directory("dir")
assert(dir.parent.parent.asString == Directory.current.asString);
dir.parent.removeAll;
\end{urbiscript}

\item[remove]
  Remove the directory only if it is empty.
\begin{urbiscript}
dir = Directory.create("dir")|;
File.create("dir/file")|;
dir.remove;
[00000001:error] !!! remove: directory not empty: "dir"
dir.clear;
dir.remove;
assert(!dir.exists);
\end{urbiscript}

\item[removeAll]
  Remove all children recursively including the directory itself.
\begin{urbiscript}
dir1 = Directory.create("dir1")|;
dir2 = Directory.create("dir1/dir2")|;
var file1 = File.create("dir1/file1")|;
var file2 = File.create("dir1/dir2/file2")|;
dir1.removeAll;
assert(!dir1.exists);
\end{urbiscript}

\item[rename]
  Rename or move the directory. Return the modified directory.
\begin{urbiscript}
dir = Directory.create("dir")|;
File.create("dir/file")|;
dir.rename("other");
[00000001] Directory("other")
dir.content;
[00000002] ["file"]
dir2 = Directory.create("dir2")|;
dir.rename("dir2/other2");
[00000003] Directory("dir2/other2")
dir.content;
[00000004] ["file"]
dir2.removeAll;
\end{urbiscript}

\end{urbiscriptapi}


%%% Local Variables:
%%% mode: latex
%%% TeX-master: "../urbi-sdk"
%%% ispell-dictionary: "american"
%%% ispell-personal-dictionary: "../urbi.dict"
%%% fill-column: 76
%%% End:
