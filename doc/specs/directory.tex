%% Copyright (C) 2009-2011, Gostai S.A.S.
%%
%% This software is provided "as is" without warranty of any kind,
%% either expressed or implied, including but not limited to the
%% implied warranties of fitness for a particular purpose.
%%
%% See the LICENSE file for more information.

\section{Directory}

A \dfn{Directory} represents a directory of the file system.

\subsection{Prototypes}
\begin{refObjects}
\item[Object]
\end{refObjects}

\subsection{Construction}

A \dfn{Directory} can be constructed with one argument: the path of
the directory using a \refObject{String} or a \refObject{Path}. It can
also be constructed by the method open of \refObject{Path}.

\begin{urbiscript}
Directory.new(".");
[00000001] Directory(".")
Directory.new(Path.new("."));
[00000002] Directory(".")
\end{urbiscript}

\subsection{Slots}
\begin{urbiscriptapi}
\item['/'](<str>)
  \experimental{}

  The \var{str} \refObject{String} is concatenated with the directory path.
  If the resulting path is either a directory or a file, \refSlot{'/'} will
  returns either a \refObject{Directory} or a \refObject{File} object.
\begin{urbiscript}
var dir1 = Directory.create("dir1")|;
var dir2 = Directory.create("dir1/dir2")|;
var file = File.create("dir1/file")|;
dir1 / "dir2";
[00000001] Directory("dir1/dir2")
dir1 / "file";
[00000002] File("dir1/file")
\end{urbiscript}
\begin{urbicomment}
removeFs(dir1);
removeSlots("dir1", "dir2", "file");
\end{urbicomment}


\item['<<'](<entity>)
  \experimental{}

  If \var{entity} is a \refObject{Directory} or a \refObject{File},
  \refSlot{'<<'} copies \var{entity} in the \this directory.
  Return \this to allow chained operations.
\begin{urbiscript}
var dir1 = Directory.create("dir1")|;
var dir2 = Directory.create("dir2")|;
var file = File.create("file")|;
dir1 << file << dir2;
[00000001] Directory("dir1")
dir1.content;
[00000003] ["dir2", "file"]
dir2;
[00000004] Directory("dir2")
file;
[00000005] File("file")
\end{urbiscript}
\begin{urbicomment}
removeFs(dir1, dir2, file);
removeSlots("dir1", "dir2", "file");
\end{urbicomment}


\item[asList]
  The contents of the directory as a \refObject{Path} list.  The
  various paths include the name of the directory \this.


\item[asPath] A \refObject{Path} being the path of the directory.


\item[asString] A \refObject{String} containing the path of the directory.
\begin{urbiassert}
Directory.new(".").asString == ".";
\end{urbiassert}


\item[basename] Return a \refObject{String} containing the path of the
  directory without its dirname.  Deprecated, use
  \lstinline|asPath.basename| instead.
\begin{urbiassert}[firstnumber=1]
var dir1 = Directory.create("dir1");
var dir2 = Directory.create("dir1/dir2");

dir1.basename == dir1.asPath.basename == "dir1";
[00000808:warning] !!! `basename' is deprecated, use `asPath.basename'
dir2.basename == dir2.asPath.basename == "dir2";
[00000811:warning] !!! `basename' is deprecated, use `asPath.basename'
\end{urbiassert}
\begin{urbicomment}
removeFs("dir1");
\end{urbicomment}


\item[clear]
  Remove all children recursively but not the directory itself. After a
  call to \refSlot{clear}, a call to \refSlot{empty} should return
  \lstinline|true|.
\begin{urbiassert}
var dir1 = Directory.create("dir1");
var dir2 = Directory.create("dir1/dir2");
var file1 = File.create("dir1/file1");
var file2 = File.create("dir1/dir2/file2");

dir1.content == ["dir2", "file1"];
dir2.content == ["file2"];
dir1.clear.isVoid;
dir1.empty;
\end{urbiassert}
\begin{urbicomment}
removeFs("dir1");
\end{urbicomment}


\item[content]
  The contents of the directory as a \refObject{String} list.  The
  strings include only the last component name; they do not contain
  the directory name of \this.


\item[copy](<dirname>)
  Copy recursively all items of the \this directory
  into the directory \var{dirname} after creating it.
\begin{urbiscript}
var dir1 = Directory.create("dir1")|;
var dir2 = Directory.create("dir1/dir2")|;
var file = File.create("dir1/file")|;
var directory1 = dir1.copy("directory1")|;
dir1;
[00000002] Directory("dir1")
directory1.content;
[00000003] ["dir2", "file"]
\end{urbiscript}
\begin{urbicomment}
removeFs(dir1, directory1);
removeSlots("dir1", "dir2", "file", "directory1");
\end{urbicomment}


\item[copyInto](<dirname>)
  \experimental{}

  Copy \this into \var{dirname} without creating it.
\begin{urbiscript}
var dir = Directory.create("dir")|;
var dir1 = Directory.create("dir1")|;
var dir2 = Directory.create("dir1/dir2")|;
var file = File.create("dir1/file")|;
dir1.copyInto(dir);
[00000001] Directory("dir/dir1")
dir1;
[00000002] Directory("dir1")
dir1.content;
[00000003] ["dir2", "file"]
dir.content;
[00000004] ["dir1"]
Directory.new("dir/dir1").content;
[00000005] ["dir2", "file"]
\end{urbiscript}
\begin{urbicomment}
removeFs(dir, dir1);
removeSlots("dir1", "dir2", "file", "dir");
\end{urbicomment}


\item[create](<name>)
  Create the directory \var{name} where \var{name} is either a
  \refObject{String} or a \refObject{Path}. In addition to system errors that
  can occur, errors are raised if directory or file \var{name} already exists.
\begin{urbiscript}
Directory.new("dir");
[00000001:error] !!! new: no such file or directory: dir
var dir = Directory.create("dir");
[00000002] Directory("dir")
dir = Directory.create("dir");
[00000001:error] !!! create: directory already exists: dir
dir.content;
[00000003] []
\end{urbiscript}
\begin{urbicomment}
removeFs(dir);
removeSlots("dir");
\end{urbicomment}


\item[createAll](<name>)
  Create the directory \var{name} where \var{name} is either a
  \refObject{String} or a \refObject{Path}. If \var{name} is a
  path (or a \refObject{String} describing a path) no errors are
  raised if one directory doesn't exist or already exists. Instead
  \refSlot{createAll} creates them all as in the Unix \samp{make -p} command.
\begin{urbiscript}
Directory.create("dir1/dir2/dir3");
[00000001:error] !!! create: no such file or directory: dir1/dir2/dir3
var dir1 = Directory.create("dir1");
[00000002] Directory("dir1")
Directory.createAll("dir1/dir2/dir3");
[00000002] Directory("dir1/dir2/dir3")
\end{urbiscript}
\begin{urbicomment}
removeFs(dir1);
removeSlots("dir1");
\end{urbicomment}


\item[empty]
  Whether the directory is empty.
\begin{urbiassert}
var dir = Directory.create("dir");
dir.empty;
File.create("dir/file");
!dir.empty;
\end{urbiassert}
\begin{urbicomment}
removeFs("dir");
\end{urbicomment}


\item[exists]
  Whether the directory still exists.
\begin{urbiassert}
var dir = Directory.create("dir");
dir.exists;
dir.remove.isVoid;
!dir.exists;
\end{urbiassert}


\item[fileCreated](<name>)%
  Event launched when a file is created inside the directory.
  May not exist if not supported by your architecture.

%% firstline is used to separate inotify test from the others, since they
%% are skipped on architectures such as Mac OS X.
\begin{urbiscript}[firstnumber=1]
if (Path.new("./dummy.txt").exists)
  File.new("./dummy.txt").remove;

  {
    var d = Directory.new(".");
    waituntil(d.fileCreated?(var name));
    assert
    {
      name == "dummy.txt";
      Path.new(d.asString + "/" + name).exists;
    };
  }
&
  {
    sleep(100ms);
    File.create("./dummy.txt");
  }|;
\end{urbiscript}


\item[fileDeleted](<name>)%
  Event launched when a file is deleted from the directory.  May not exist
  if not supported by your architecture.
\begin{urbiscript}[firstnumber=1]
if (!Path.new("./dummy.txt").exists)
  File.create("./dummy.txt")|;

  {
    var d = Directory.new(".");
    waituntil(d.fileDeleted?(var name));
    assert
    {
      name == "dummy.txt";
      !Path.new(d.asString + "/" + name).exists;
    };
  }
&
  {
    sleep(100ms);
    File.new("./dummy.txt").remove;
  }|;
\end{urbiscript}
%% Use firstline after this test if this is not related to inotify.


\item[lastModifiedDate]
  \experimental{}

  Return a \refObject{Date} object stating when the directory was last
  modified.  Deprecated, use
  \lstinline|asPath.lastModifiedDate| instead.


\item[moveInto](<dirname>)
  \experimental{}

  Move \this into \var{dirname} without creating it.
%% firstnumber=1 so that these tests are run even on machines with inotify
%% support.
\begin{urbiscript}[firstnumber=1]
var dir1 = Directory.create("dir1")|;
var dir2 = Directory.create("dir1/dir2")|;
var file = File.create("dir1/file")|;
var dir = Directory.create("dir")|;
dir1.moveInto(dir);
[00000001:warning] !!! `rename(name)' is deprecated, use `asPath.rename(name)'
dir1;
[00000002] Directory("dir/dir1")
assert
{
  dir1.content == ["dir2", "file"];
  dir.content == ["dir1"]
};
\end{urbiscript}
\begin{urbicomment}
removeFs(dir);
removeSlots("dir1", "dir2", "file", "dir");
\end{urbicomment}


\item[parent]
  The Directory parent of \this.
\begin{urbiscript}
Directory.create("dir")|;
var dir = Directory.create("dir/dir")|;
dir.parent;
[00000001] Directory("dir")
assert(dir.parent.parent.asString == Directory.current.asString);
\end{urbiscript}
\begin{urbicomment}
removeFs(dir.parent);
removeSlots("dir");
\end{urbicomment}


\item[remove]%
  Remove \this directory if it is empty.
\begin{urbiscript}
var dir = Directory.create("dir")|;
File.create("dir/file")|;
dir.remove;
[00000001:error] !!! remove: directory not empty: dir
dir.clear;
dir.remove;
assert(!dir.exists);
\end{urbiscript}
\begin{urbicomment}
removeSlots("dir");
\end{urbicomment}


\item[removeAll]
  Remove all children recursively including the directory itself.
\begin{urbiscript}
var dir1 = Directory.create("dir1")|;
var dir2 = Directory.create("dir1/dir2")|;
var file1 = File.create("dir1/file1")|;
var file2 = File.create("dir1/dir2/file2")|;
dir1.removeAll;
assert(!dir1.exists);
\end{urbiscript}
\begin{urbicomment}
removeSlots("dir1", "dir2", "file1", "file2");
\end{urbicomment}


\item[rename] Rename or move the directory. Deprecated, use
  \lstinline|asPath.rename| instead.
\begin{urbiscript}
var dir = Directory.create("dir")|;
File.create("dir/file")|;
dir.rename("other");
[00000001:warning] !!! `rename(name)' is deprecated, use `asPath.rename(name)'
dir;
[00000002] Directory("other")
dir.content;
[00000003] ["file"]
var dir2 = Directory.create("dir2")|;
dir.rename("dir2/other2");
[00000002:warning] !!! `rename(name)' is deprecated, use `asPath.rename(name)'
dir;
[00000005] Directory("dir2/other2")
dir.content;
[00000006] ["file"]
\end{urbiscript}
\begin{urbicomment}
removeFs(dir, dir2);
removeSlots("dir", "dir2");
\end{urbicomment}


\item[size]
  \experimental{}

  The size of all the directory content computed recursively, in number of
  bytes.
\begin{urbiscript}
var dir = Directory.create("dir")|;
Directory.create("dir/dir")|;
File.save("dir/file", "content");
var file1 = File.create("dir/file")|;
File.save("dir/dir/file", "content");
var file2 = File.create("dir/dir/file")|;
assert(dir.size() == file1.size() + file2.size());
\end{urbiscript}
\begin{urbicomment}
removeFs(dir);
removeSlots("dir", "file1", "file2");
\end{urbicomment}
\end{urbiscriptapi}

%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: "../urbi-sdk"
%%% ispell-dictionary: "american"
%%% ispell-personal-dictionary: "../urbi.dict"
%%% fill-column: 76
%%% End:
