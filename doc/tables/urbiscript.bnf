#:
#:    Be very careful: this file is used by the \grammar macro which
#:    takes a list of intervals of snippets to include.  The file is
#:    read only once, so the intervals must be successive: there is a
#:    *silent* failure to include chunks that were *before*.  E.g.,
#:    \grammar{class-expression,lvalue-end} will fail to include the
#:    definition of <lvalue> if lvalue-end is before class-expression.
#:
#:    So define the dependencies after the dependant.
#:
#:
#:class
<class-expression>
  ::= "class" <lvalue> (":" <prototypes>)? <block>

<prototypes>
  ::= (<expression> ",")* <expression>
#:exp

#:lvalue
<lvalue>
  ::= (<primary-expression> ".")* <identifier>
#:end

#:primary
<primary-expression>
  ::= <assert-expression>
    | <block>
    | "call"
    | <do-expression>
    | <function-expression>
    | <getslot-expression>
    | <literal-expression>
    | <postfix-expression>
    | <property-expression>
    | "this"
    | <watch-expression>
    | "(" <expression> ")"
#:exp

#:assert
<assert-expression>
  ::= "assert" "(" <expression> ")"
    | "assert" "{" (<expression> (";" <expression>)* ";"?)? "}"
#:exp

#:do
<do-expression>
  ::= "do" "(" <expression> ")" <block>
#:exp

#:getslot
<getslot-expression>
  ::= (<primary-expression> ".")? "&" <identifier>
#:exp

#:postfix
<postfix-expression>
  ::= <lvalue> "++"
    | <lvalue> "--"
#:exp

#:property
<property-expression>
  ::= <lvalue> "->" <identifier>
#:exp

#:watch
<watch-expression>
  ::= "watch" "(" <expression> ")"
#:exp

#:comparison
<comparison-expression>
  ::= <arithmetic-expression> (<comparison-operator> <arithmetic-expression>)*
<comparison-operator>
  ::= "!=" | "!==" | "<" | "<=" | "==" | "===" | "=~=" | ">" | ">=" | "~="
#:exp

#:arithmetic
<arithmetic-expression>
  ::= <unary-expression>
    | <arithmetic-expression> <arithmetic-operator> <arithmetic-operator>
<arithmetic-operator>
   ::= "+" | "-" | "*" | "/" | "%"
#:exp

#:assign
<assign-expression>
  ::= <expression> "=" <expression> <modifiers>?
    | <lvalue> <inplace-operator> <expression>
<inplace-operator>
   ::= "+=" | "-=" | "*=" | "/=" | "^=" | "%="
#:exp

#:function
<function-declaration>
  ::= <closure-or-function> <compound-name> <formals>? <block>

<function-expression>
  ::= <closure-or-function> <formals>? <block>

<closure-or-function>
  ::= "closure" | "function"

<formals>
  ::= "(" (<formal> ("," <formal>)* ","?)? ")"

<formal>
  ::= "var"? <identifier> ("=" <expression>)?
    | "var"? <identifier> "[" "]"
#:end

#:stm
<statement>
  ::= <at-statement>
    | <every-statement>
    | <for-statement>
    | <loop-statement>
    | <tag-statement>
    | <timeout-statement>
    | <try-statement>
    | <while-statement>
#:end

#:at
<at-statement>
   ::= "at" ("async" | "sync")? "(" <expression> ("~" <expression>)? ")"
       <statement> <onleave-clause>?
     | "at" ("async" | "sync")? "(" <event-clause> ")"
       <statement> <onleave-clause>?

<event-clause>
    ::= <expression> "?" <arguments>? ("~" <expression>)? ("if" <expression>)?

<arguments>
    ::= "(" (<expression> ("," <expression>)* ","?)? ")"

<onleave-clause>
  ::= "onleave" <statement>
#:stm

#:forc
<for-statement>
   ::= "for"  "(" <statement>? ";" <expression> ";" <statement>? ")" <statement>
     | "for;" "(" <statement>? ";" <expression> ";" <statement>? ")" <statement>
     | "for|" "(" <statement>? ";" <expression> ";" <statement>? ")" <statement>
#:stm

#:foreach
<for-statement>
   ::= "for"  "(" "var" <identifier> ("in" | ":") <expression> ")" <statement>
     | "for;" "(" "var" <identifier> ("in" | ":") <expression> ")" <statement>
     | "for|" "(" "var" <identifier> ("in" | ":") <expression> ")" <statement>
     | "for&" "(" "var" <identifier> ("in" | ":") <expression> ")" <statement>
#:stm

#:forn
<for-statement>
   ::= "for"  "(" <expression> ")" <statement>
     | "for;" "(" <expression> ")" <statement>
     | "for|" "(" <expression> ")" <statement>
     | "for&" "(" <expression> ")" <statement>
#:stm

#:if
<if-statement>
  ::= "if" "(" <statements> ")" <statement> <else-clause>?
#:stm

#:loop
<loop-statement>
  ::= "loop"  <statement>
    | "loop;" <statement>
    | "loop|" <statement>
#:stm

#:return
<return-statement>
  ::= "return" <expression>?
#:stm

#:tag
<tag-statement>
  ::= <primary-expression> ":" <statement>
#:stm

#:timeout
<timeout-statement>
  ::= "timeout" "(" <float> ")" <statement>
      <catch-all-clause>? <else-clause>? <finally-clause>?
#:stm

#:try
<try-statement>
  ::= "try" "{" <statement>* "}" <catch-clause>+ <else-clause>? <finally-clause>?
    | "try" "{" <statement>* "}" <finally-clause>
#:stm

#:while
<while-statement>
  ::= "while"  "(" <expression> ")" <statement>
    | "while;" "(" <expression> ")" <statement>
    | "while|" "(" <expression> ")" <statement>
#:stm

#:catch
<catch-clause>
  ::= "catch" "(" <pattern> ")" <block>
    | <catch-all-clause>

#:catchall
<catch-all-clause>
  ::= "catch" <block>
#:clause

#:else
<else-clause>
  ::= "else" <statement>
#:clause

#:finally
<finally-clause>
  ::= "finally" <block>
#:clause

#:every
<every-statement>
  ::= "every"  "(" <expression> ")" <statement>
    | "every|" "(" <expression> ")" <statement>
    | "every," "(" <expression> ")" <statement>
#:stm

#:literal
<literal-expression>
  ::= <dictionary-literal>
    | <list-literal>
    | <number-literal>
    | <string-literal>
#:exp

#:dictionary
<dictionary-literal>
  ::= "[" (<dictionary-entry> ("," <dictionary-entry>>)* ","?)? "]"
<dictionary-entry>
  ::= <expression> "=>" <expression>
#:literal

#:list
<list-literal>
  ::= "[" (<expression> ("," <expression>)* ","?)? "]"
#:literal

#:identifier
<identifier>
  ::= (<letter> | "_") (<letter> | <digit> | "_")*

<letter>
  ::= [a-zA-Z]

<digit>
  ::= [0-9]
#:end

#:block
<block>
  ::= "{" <statement>* "}"
#:exp
