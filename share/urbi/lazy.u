/*
 * Copyright (C) 2008-2012, Gostai S.A.S.
 *
 * This software is provided "as is" without warranty of any kind,
 * either expressed or implied, including but not limited to the
 * implied warranties of fitness for a particular purpose.
 *
 * See the LICENSE file for more information.
 */

requireFile("urbi/global.u");
requireFile("urbi/comparable.u");

package lang.Lazy : Comparable
{
  var code;
  var val;

  function init(value)
  {
    var this.code = value |
    var this.val
  };

  function '=='(other)
  {
    // code is very likely to be a closure, do not fire it, otherwise
    // we might compare two values instead of two expressions.
    getSlotValue("code") == other.getSlotValue("code")
      && getSlotValue("val").acceptVoid == other.getSlotValue("val").acceptVoid;
  };

  function eval()
  {
    val = code.acceptVoid|
    getSlotValue("val").unacceptVoid
  };

  function value()
  {
    if (getSlotValue("val").isVoid)
      eval|
    getSlotValue("val").unacceptVoid
  };

  function asString ()
  {
    var c = getSlotValue("code") |
    if (c.isVoid)
      type
    else if (c.hasSlot("bodyString"))
      c.bodyString
    else
      c.asString
  };
};

package lang.PseudoLazy: Lazy
{

  function eval()
  {
    val = getSlotValue("code").acceptVoid|
    getSlotValue("val").unacceptVoid
  };

};
