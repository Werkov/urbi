class Global.Dictionary
{
  var protoName = "Dictionary";
  var content = (||);

  function set (key, value)
  {
    if (has((key)))
      _updateSlot (content, key, value)
    else
      _setSlot (content, key, value)|
    return self|
  };

  function get (key)
  {
    _getSlot(content, key)
  };

  function has (key)
  {
    !_isNil(_locateSlot (content, key))
  };

  function flush ()
  {
    keys.each(function (key) {
      _removeSlot(content, key)
    }.makeClosure)|
    self|
  };

  function keys ()
  {
    _slotNames (content)
  };

  function asList ()
  {
    var res = []|
    keys.each (function(k) {
      res.push_back(Pair.new (k, get (k)))
    }.makeClosure)|
    res
  };

  function asString ()
  {
    if (self === Dictionary)
      "<Dictionary>"
    else
    {
      var res = asList.sort|
      res = res.map(function (p) {
        p.first.asPrintable + " => " + p.second.asPrintable
      })|
      "Dictionary {" + res.join(", ") + "}"
    }
  };
};
