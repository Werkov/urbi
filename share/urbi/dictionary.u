do Global.Dictionary
{
  /// Dictionary.init(KEY1, VAL1, ...).
  // FIXME: throw on invalid arg number.
  function init
  {
    for| (var i = 0; i < call.argsCount; i += 2)
      set(call.evalArgAt(i), call.evalArgAt(i + 1)) |
    this
  };

  function asList ()
  {
    var res = []|
    for (var k: keys)
      res.push_back(Pair.new (k, get (k)))|
    res
  };

  function asString ()
  {
    if (this === Dictionary)
      "<Dictionary>"
    else
    {
      var res = asList.sort|
      res = res.map(function (p) {
        p.first.asPrintable + " => " + p.second.asPrintable
      })|
      "Dictionary {" + res.join(", ") + "}"
    }
  };

  function getWithDefault(name, otherwise)
  {
    if (has(name))
      get(name)
    else
      otherwise
  };

  // Syntactic sugar.
  copySlot("set", "[]=");
  copySlot("get", "[]");
};
