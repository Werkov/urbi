/*----------.
| Channel.  |
`----------*/

function Object.print()
{
  topLevel << this
};

/* Implemented in C++ for better performances.
function lobby.send
{
  var what = call.evalArgAt(0);
  var tag = "";
  if (call.argsCount > 1)
    tag = call.evalArgAt(1);
  write("[" + time.trunc
        + { if (tag == "") "" else ":" + tag}
        + "] " + what + "\n")
};
*/

function Global.echo
{
  var tag = ""|
  if (1 < call.argsCount)
    tag = call.evalArgAt(1)|
  lobby.send("*** " + call.evalArgAt(0).asString, tag)
};

function Global.wall
{
  call.message = "echo";
  for (var lobby in lobbies - [Lobby])
    lobby.callMessage(call);
};

// Does not have anything special.  Yet.
class Global.Channel
{
  /// Display nothing if false.
  var enabled = true;
  /// Use asTopLevelPrintable if set (display the string 'foo' as '"foo"')
  var quote = true;

  /// By default, the Channel prototype has no tag.
  var name = "";

  function init(x)
  {
    var this.name = x
  };

  function '<<'(x)
  {
    if (enabled)
    {
      var toPrint = { if (quote) x.asToplevelPrintable else x.asString }|
      if (!toPrint.isNil)
	lobby.send(toPrint, name);
    }
  };
};

// Default channels.
do (Global)
{
  // Long names are k1 compatible.
  var output  = Channel.new("output");
  var error   = Channel.new("error");
  error.quote = false;
  var warning = Channel.new("warning");
  warning.quote = false;
  var clog    = Channel.new("clog");
  // This one is used by urbiimage.
  var uimg    = Channel.new("uimg");

  // Short names are user compatible.
  var cout    = output;
  var cerr    = error;

  // Top-levl channel
  var topLevel = Channel.new("");
  Lobby.setProtos([topLevel]); //Now 'lobby << 1' displays in "lobby"'s context.

  function warn(msg) { echo (msg, "warning") };
};


{} // Global.asString is not yet defined: evaluate to void to avoid an error.
