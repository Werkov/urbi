/*
 * Copyright (C) 2010, Gostai S.A.S.
 *
 * This software is provided "as is" without warranty of any kind,
 * either expressed or implied, including but not limited to the
 * implied warranties of fitness for a particular purpose.
 *
 * See the LICENSE file for more information.
 */

class System.PackageInfo
{
  var components = [ => ];

  var dependencies = [];
  function init (var name)
  {
    var this.name = name |
    System.PackageInfo.components[name] = this
  };

  function depend(var other)
  {
    dependencies += [other]|;
  };

  function signature ()
  {
    "%s version %s rev. %s\n"
    "Copyright (C) %s %s"
    % [name, version, revision, copyright_years, copyright_holder]
    +
    {
      if (dependencies)
        "\n\n"
        + dependencies.map(methodToFunction("signature")).join("\n")
      else
        "";
    }
  };

  function banner ()
  {
    "**********************************************************\n"
    "%s\n"
    "\n"
    "%s comes with ABSOLUTELY NO WARRANTY.\n"
    "This software can be used under certain conditions;\n"
    "see LICENSE file for details.\n"
    "\n"
    "See http://www.urbiforge.com for news and updates.\n"
    "**********************************************************"
    % [signature, name]
  };
};

load("urbi/package-info-urbi-sdk.u");
load("urbi/package-info-urbi-sdk-remote.u");
load("urbi/package-info-libport.u");

do (System.PackageInfo)
{
  var copyright_holder = "@PACKAGE_COPYRIGHT_HOLDER@";
  var copyright_years = "@PACKAGE_COPYRIGHT_YEARS@";
  do (components["Urbi SDK"])
  {
    depend(components["Urbi SDK Remote"]);
  };

  do (components["Urbi SDK Remote"])
  {
    depend(components["Libport"]);
  }
};
