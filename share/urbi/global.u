do (Global)
{
  // Import the primitives.
  addProto(System);

  function echo
  {
    var tag = ""|
    if (1 < call.argsCount)
      tag = call.evalArgAt(1)|
    lobby.send("*** " + call.evalArgAt(0).asString, tag)
  };

  /// Returns true if argument is defined. Argument can be of the form a.b.c.
  function isdef
  {
    var base = call.sender |
    var first = true |
    for| (var c: call.argString(0).split([".", " "], nil, false, false))
    {
      if (base.hasSlot(c))
      {
        if (!first && !base.hasLocalSlot(c))
          return false |
        var s = base.getSlot(c).acceptVoid |
        base = s.apply([s]).acceptVoid |
        first = false
      }
        else
        return false |
    } |
    return true
  };

  /*---------------------------.
  | Method function wrappers.  |
  `---------------------------*/

  // Make a function f from a method m, so as f(a, b, c) is equivalent
  // to a.m(b, c)
  // FIXME: I'm not satisfied with the name, but can't find anything
  // better
  function methodToFunction(name)
  {
    function
    {
      var args = call.evalArgs |
      var val = args.head.getSlot(name) |
      if (val.isA(Executable))
        return val.apply(args)
      else
        return val |
    }
  };

  function wall
  {
    call.message = "echo";
    for (var lobby in lobbies - [Lobby])
      lobby.callMessage(call);
  };
};
