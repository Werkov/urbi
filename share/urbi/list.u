## ----- ##
## List. ##
## ----- ##

do List
{
  // Whether e is in the list.
  function has(e)
  {
    for| i in self
    {
      if (i == e)
	return true
    } |
    return false
  };

  function hasSame(e)
  {
    for| i in self
    {
      if (i === e)
        return true
    } |
    return false
  };

  // Same contents except occurrences of e.
  function remove(e)
  {
    var res = self - [e] |
    clear |
    for| i in (res) {
      push_back (i)
    } |
    res
  };

  // Subtract a list from self.
  function '-' (rhs)
  {
    var res = [] |
    for| i in self
    {
      if (!rhs.has(i))
	res.push_back(i)
    } |
    return res
  };

  function empty
  {
    return size == 0
  };

  # The Lisp-like name.
  copySlot ("front", "head");

  function insert(e)
  {
    echo ("List.insert is deprecated, please use List.push_back") |
    push_back(e)
  };

  # Concatenate the string representation of the members, separated by \a sep
  function join(sep)
  {
    if (empty)
      ""
    else
      tail.foldl(function(a, b) { a + sep + b.asString}.capture("sep"), head.asString);
  };

  ## ------------------------ ##
  ## Functional programming.  ##
  ## ------------------------ ##

  # Build a new list containing the result of \a f applied to all elements
  function map (f)
  {
    var res = [] |
    for| i in self { res.push_back (f (i)) } |
    res
  };

  function foldl(action, value)
  {
    var res = value|
    for (var l = self; !l.empty; l = l.tail)
      res = action(res, l.head)|
    res
  };

  function foldl_head(action)
  {
    tail.foldl(getSlot("action"), head)
  };

  # Apply \a f on every element
  function each (f)
  {
    for| i in self { f (i) } |
    void
  };

  # Check whether at least one element verifies the \a f predicate
  function any (f)
  {
    for i in self { if (f (i)) return true } |
    false
  };

  # Check whether all elements verifies the \a f predicate
  function all (f)
  {
    for i in self { if (!f (i)) return false } |
    true
  };

  # Get the string representation. We want "void" and "nil" to
  # be printed as part of a list, so we add a "asStringForList"
  # method to everyone.
  function Object.asStringForList () { asString };
  var nil.asStringForList = "nil";
  var void.asStringForList = "void";

  function asString ()
  {
    if (self === List)
      return "<List>" |
    var res = "" |
    if (self.size)
    {
      res = self.front.asStringForList |
      for| elt in self.tail
      {
        res += ", " + getSlot("elt").asStringForList |
      }
    } |
    "[" + res + "]"
  };
};
