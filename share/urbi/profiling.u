/*-----------------------.
| Profiling operations.  |
`-----------------------*/

class Global.Profiling
{
  /// timen(CODE, #ITERATIONS).
  /// e.g. timen(2+2, 42).
  function timen
  {
    echo (self.new(call.argAt(1), call.argAt(2)))
  };

  /// timen(CODE, #ITERATIONS).
  /// e.g. init(2+2, 42).
  function init
  {
    var self.expr   = call.argString(1);
    var self.niters = call.evalArgAt (2);
    var start_time = time;
    var start_cycle = cycle |

    // Bench.
    for (niters)
      call.evalArgAt (1) |

    var self.ncycles = cycle - start_cycle - 1;
    var self.expired = time - start_time;
  };

  // Display the results.
  function asString()
  {
      "Profiling information\n"
    + "  Expression:       " + expr + "\n"
    + "  Iterations:       " + niters + "\n"
    + "  Cycles:           " + ncycles + "\n"
    + "  Total time:       " + expired + " ms\n"
    + "  Single iteration: " + expired / niters + " ms\n"
    + "                    " + ncycles / niters + " cycles";
  };
};

