/*
 * Copyright (C) 2010, Gostai S.A.S.
 *
 * This software is provided "as is" without warranty of any kind,
 * either expressed or implied, including but not limited to the
 * implied warranties of fitness for a particular purpose.
 *
 * See the LICENSE file for more information.
 */

class Global.Control
{
  function detach
  {
    spawn(closure() { call.evalArgAt(0) }, false)
  };

  function disown
  {
    spawn(closure() { call.evalArgAt(0) }, true)
  };


  // 'every,sleep'(deadline, delay)
  //
  // Prepare the next iteration for "every,", which should happen at
  // deadline + delay.
  function 'every,sleep'(deadline, delay)
  {
    deadline += delay |
    sleep(deadline - shiftedTime)|
    deadline
  };

  // 'every|sleep'(deadline, delay)
  //
  // Prepare the next iteration for every|, which should happen at
  // deadline + delay.  Return deadline.
  function 'every|sleep'(deadline, delay)
  {
    deadline += delay |
    var delta = deadline - shiftedTime |
    if (0 < delta)
      sleep(delta)
    else
      deadline -= delta |
    deadline
  };

  // whenever_(test, on_true, on_false)
  //
  // Takes three expressions, a test and some code to execute each
  // time the test is true and some code to execute each time the test
  // is false.
  function whenever_
  {
    detach ({
      loop call.evalArgAt ({ if (call.evalArgAt (0)) 1 else 2 })
    })|;
  };

  // persist takes an expression and a delay, and returns an object
  // whose val slot evaluates to true if the expression has been
  // continously true for this delay and false otherwise.
  //
  // This function is used to implement
  //   at (condition ~ delay) action [onleave leaveaction]
  // as
  //   var u = persist (condition, delay);
  //   at (u.val) action [onleave leaveaction]
  //
  // The "persist" action will be controlled by the same tags as
  // the initial "at" block.
  function persist
  {
    var res = false |
    var tag = Tag.new("persist".fresh) |
    var delay = call.evalArgAt (1) |
    Control.at_ (call.evalArgAt (0),
                 detach({tag: { sleep (delay) | res = true }}),
                 detach({ res = false | tag.stop })) |
    function () { res }
  };

  function finally(action, reaction)
  {
    var s = Semaphore.new(0) |
    var t = Tag.new |
    // We want to ensure that reaction is called on the end no matter what,
    // so we must disown from the tag stack.
    // We do not know how long disown takes to start, so be safe and use a lock.
    disown({
      {waituntil(t.leave?) | reaction()}
      & s.release
           }) &
    {
      s.acquire |
      t: action()
    }
  };
};

// Provide these at the Global level.
do (Global)
{
  var detach  = Control.getSlot("detach");
  var disown  = Control.getSlot("disown");
  var persist = Control.getSlot("persist");
}|;
