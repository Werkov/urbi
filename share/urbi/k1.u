# k1 compatibility.
class Global.Kernel1Functions
{

  // isdef("name"): Check whether "name" is defined (here or above).
  function isdef
  {
    !call.target.locateSlot(call.argString(1)).isNil;
  };

  function ping ()
  {
    echo ("pong time=" + time.asString);
  };

  function seq(n)
  {
    warn("`seq(number)' is deprecated, use `number.seq'");
    n.seq
  };

  function size(l)
  {
    warn("`size(list)' is deprecated, use `list.size'");
    l.size
  };

  function strlen(s)
  {
    s.length
  };
};
Global.addProto(Global.Kernel1Functions);

class Global.Kernel1Methods
{
  ## --------------------------------- ##
  ## Dirty tricks to change foo.load.  ##
  ## --------------------------------- ##

  function on()
  {
    self.load = 1;
    self
  };

  function off()
  {
    self.load = 0;
    self
  };

  function switch()
  {
    if (self.load)
      self.load = 0
    else
      self.load = 1;
    self
  };
};
Object.addProto(Global.Kernel1Methods);



# Deprecated command from k1.
# FIXME: Some commands should be rewritten once it becomes possible.
class Global.DeprecatedCommands
{
  var deprecated_ = function () { echo("`" + name + "' is deprecated." ) };

  function deprecate(slotName)
  {
    self.setSlot(slotName, self.getSlot("deprecated_").clone());
    self.getSlot(slotName).capture("name", slotName);
  };

  for i in
  [
    "commands",
    "connections",
    "devices",
    "events",
    "functions",
    "reset",
    "runningcommands",
    "strict",
    "taglist",
    "undefall",
    "unstrict",
    "uservars",
    "vars"
  ]
  {
    deprecate(i)
  };
};

Global.addProto(Global.DeprecatedCommands);
