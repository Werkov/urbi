// k1 compatibility.
class Global.Kernel1Functions
{
  function copy(o)
  {
    warn("`copy(o)' is deprecated, use `o.copy'") |
    o.copy
  };

  function noop()
  {
    warn("`noop' is deprecated, use `{}'") |
  };

  function ping()
  {
    echo ("pong time=" + (time / 1s).asString + "s");
  };

  function seq(n)
  {
    warn("`seq(number)' is deprecated, use `number.seq'") |
    n.seq
  };

  function size(l)
  {
    warn("`size(list)' is deprecated, use `list.size'") |
    l.size
  };

  function strlen(s)
  {
    warn("`strlen(string)' is deprecated, use `string.length'") |
    s.length
  };

  function isvoid
  {
    call.evalArgAt(0).acceptVoid.isVoid
  };

  // Deprecated commands from k1.
  // FIXME: Maybe some commands should be rewritten.
  for| (var name: [
                  "commands",
                  "connections",
                  "devices",
                  "events",
                  "functions",
                  "reset",
                  "runningcommands",
                  "strict",
                  "taglist",
                  "undefall",
                  "unstrict",
                  "uservars",
                  "vars"
                    ])
    setSlot(name,
      function () { echo("`" + name + "' is deprecated." ) });

  var system = System;
  var system.ghostID = "";
};

// K1 compatibility: import the k1 functions.
Global.addProto(Global.Kernel1Functions);
