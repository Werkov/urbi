/*
 * Copyright (C) 2008-2012, Gostai S.A.S.
 *
 * This software is provided "as is" without warranty of any kind,
 * either expressed or implied, including but not limited to the
 * implied warranties of fitness for a particular purpose.
 *
 * See the LICENSE file for more information.
 */

//                                      -*- urbi -*-

clog << "Loading test-suite.u.";
clog << "This file modifies the behavior of urbiscript.";
clog << "It is not part of Urbi itself.  Do not depend on it.";

System.assertVerbose = true|;
// Too many unpredictable changes in the error messages.
Exception.Lookup.fixSpelling = false|;

// Many tests were written before the import age.
import System.*;

/*---------.
| camera.  |
`---------*/
package lang.camera: 'package'.gsrapi.Loadable
{
  UVar.new(this, "val");
  // In order to avoid the "unexpected void" messages if we forget to
  // start the camera.
  val = "!!! the camera is not load'ed";

  var images;
  function loadImages ()
  {
    if (images.isVoid())
    {
      var dir = System.urbiShareDir / "images" / "gostai-logo" / "jpeg";
      clog << "Loading images from %s." % dir;
      images =
        Directory.new(dir)
        .asList()
        .map (function (var path)
              { var res = File.new(path).content();
                res.keywords = "jpeg";
                res });
      clog << "Loaded %s images." % images.size;
    }
  };

  // Used by urbi-image for instance.
  var resolution = 0;
  var jpegfactor = 70;
  var reconstruct = 0;

  // When the camera is active, loop on the images.
  var load = false;
  var count = 0;
  at (load)
  {
    loadImages();
    every (period)
    {
      val = images[count];
      count = (count + 1) % images.size;
    }
  };
}|;


/*---------.
| Global.  |
`---------*/

do ('package'.lang)
{
  // A channel for synclines.
  var syncline = Channel.new("#")|;

  function testExit(var status, var why)
  {
    cerr << why|
    System._exit(status)
  }|;

  // The test itself declares its status as failed.
  function testFail(var why)
  {
    testExit(1, "Aborting this test: " + why)
  }|;

  // Skip this test.
  function testSkip(var why)
  {
    testExit(176, "SKIP: " + why)
  }|;

  /// Generate a stub for the LaTeX documentation of this.
  function docStub()
  {
    var res = [""];
    res << "\\section{%s}" % type;
    res << "";
    res << "\\subsection{Prototypes}";
    res << "\\begin{refObjects}";
    for (var p : protos)
      res << "\\item[%s]" % p.type;
    res << "\\end{refObjects}";
    res << "";
    res << "\\subsection{Construction}";
    res << "";
    res << "\\subsection{Slots}";
    res << "";
    res << "\\begin{urbiscriptapi}";
    for (var i : localSlotNames().sor())
    {
      res << "\\item[%s]%s%%"
             % [{ if (i.isAlnum) i else "'%s'" % i },
                { if (getSlotValue(i).isA(getSlotValue("Primitive"))) "(<arg>)" else "" }];
      res << "\\begin{urbiassert}";
      res << "";
      res << "\\end{urbiassert}";
      res << "";
    };
    res << "\\end{urbiscriptapi}";
    echo(res.join("\n"));
  };

  /*---------.
  | skipIf.  |
  `---------*/

  // Skip tests that cannot run in some configuations.
  function skipIfDebug ()
  {
    if (System.Platform.compilationModeDebug)
      testSkip("cannot run in debug compilation-mode");
  }|;

  function skipIfHostName (var host)
  {
    var hostname = System.env["BUILDFARM_HOSTNAME"] || "" |
    clog << "Hostname: %s" % hostname |
    if (hostname == host)
      testSkip("cannot run on this host (%s)" % host);
  }|;

  function skipIfRoot ()
  {
    if (System.system("test \"$(id -u)\" != 0") != 0)
      testSkip("running as root (not a good idea, really)");
  }|;

  function skipIfSpace ()
  {
    if (System.Platform.compilationModeSpace)
      testSkip("cannot run in space compilation-mode");
  }|;

  function skipIfUrbi2 ()
  {
    if (PackageInfo.components["Urbi SDK"].versionValue <= 2007000)
      testSkip("requires Urbi 3");
  }|;

  function skipIfWindows ()
  {
    if (System.Platform.isWindows())
      testSkip("cannot run on Windows");
  }|;

  function skipIfWindowsDebug ()
  {
    if (System.Platform.isWindows() && System.Platform.compilationModeDebug())
      testSkip("cannot run on Windows Debug compilation-mode");
  }|;
}|;

// Too verbose when we launch another urbi from urbi.
System.env.erase("GD_LEVEL")|;

/*---------.
| Object.  |
`---------*/

do (Object)
{
  // Deterministic localSlotNames.
  overrideSlot("localSlotNames",
    function () { 'localSlotNames$old'().sort() });

  // Remove files and directories.
  //
  // It has nothing to do this "Object", but that's for symmetry
  // with removeSlots.
  function removeFs(var names[])
  {
    for| (var s : names)
    {
      // Map Strings and Paths to Directory/File.
      if (s.isA(String))
        s = Path.new(s)|
      if (s.isA(Path))
        if (s.isDir)
          s = Directory.new(s)
        else
          s = File.new(s) |
      // Remove File/Directory.
      if (s.isA(Directory))
        s.removeAll()
      else
        s.remove()
    }|
    {}
  };

  function removeSlots(var names[])
  {
    for| (var s : names)
      // Removing names that no longer exist is no big deal.
      if (hasLocalSlot(s))
        removeSlot(s)
      else
        clog << Exception.Lookup.new(s, this, false)|
    {}
  };

}|;


/*-------------.
| Dictionary.  |
`-------------*/

// Determinism: Dictionary.asList and Dictionary.asString.
do (Dictionary)
{
  overrideSlot("keys",
    function ()
    {
      'keys$old'.sort (function (a, b) { a.asString() < b.asString() } )
    }
  )|;
  get keys() { 'keys$old'.sort (function (a, b) { a.asString() < b.asString() } )};
}|;


/*------------.
| Directory.  |
`------------*/

// These would be overridden if there is a support for inotify.
do (Directory)
{
  var fileCreated = Event.new();

  fileCreated.overrideSlot("waituntil",
    function /* skip_if_no_inotify */(var args[])
    {
      testSkip("requires inotify support");
    });

  // Deterministic content.
  overrideSlot("content",
    function () { 'content$old'.sort() });

  var fileDeleted = fileCreated;
}|;

/*--------.
| Float.  |
`--------*/

// We want random to become deterministic.
do (Float)
{
  var randomCounter = 0;
  // Chosen by fair dice roll.  Guaranteed to be random.
  // 200.map(function (v) { 100.random }).join(", ");
  var randomValue = [1, 97, 71, 28, 37, 58, 77, 97, 94, 4, 9, 31, 45,
    75, 35, 98, 42, 99, 68, 12, 60, 57, 94, 8, 95, 68, 13, 30, 6, 62,
    42, 65, 82, 52, 67, 21, 95, 12, 71, 1, 90, 31, 38, 57, 16, 90, 40,
    79, 35, 6, 72, 98, 95, 19, 54, 23, 89, 60, 5, 26, 23, 6, 13, 70,
    38, 94, 20, 44, 66, 34, 26, 94, 63, 38, 44, 90, 50, 59, 23, 47,
    85, 17, 72, 39, 47, 85, 96, 85, 23, 20, 44, 68, 35, 15, 25, 34,
    42, 11, 79, 52, 44, 95, 18, 96, 92, 15, 91, 33, 69, 97, 53, 47,
    25, 10, 62, 11, 8, 77, 61, 25, 35, 68, 95, 76, 67, 39, 74, 31, 56,
    1, 72, 60, 94, 84, 55, 89, 7, 15, 93, 69, 80, 55, 55, 6, 63, 2,
    76, 8, 49, 31, 44, 38, 8, 97, 51, 49, 3, 31, 31, 14, 19, 75, 9,
    80, 29, 23, 54, 60, 37, 45, 17, 25, 0, 56, 64, 97, 48, 4, 50, 50,
    76, 12, 54, 97, 4, 81, 48, 65, 78, 99, 9, 29, 53, 83, 47, 7, 73,
    22, 5, 76];

  overrideSlot("random",
  function ()
  {
    // We don't want COW here.
    randomValue[(Float.randomCounter++) % randomValue.size] % this;
  });
}|;


/*-----------.
| Location.  |
`-----------*/

do (Location)
{
  function here(var backtrack = 0)
  {
    var list =
   {
      try { throw Exception.new("")}
      catch (var e) { e.'$backtrace' }
    }|
    var drop = true|
    var loc = nil|
    var i = 0|
    for| (i = 0; i < list.size && loc.isNil(); ++i)
    {
      if (drop)
        drop = drop && list[i].name != "here"|
      if (!drop && backtrack++ == 0)
        loc = list[i].location|
    }|
    loc
  }
}|;


/*--------.
| Range.  |
`--------*/

class Range : Container
{
  var first;
  var last;

  function init(var f, var l)
  {
    first = f|
    last = l;
  };

  function asString()
  {
    "[%s .. %s]" % [first, last]
  };

  function has(var e)
  {
    first <= e <= last
  };
}|;


/*---------.
| String.  |
`---------*/

// Deterministic String.fresh.
do (String)
{
  var counter_ = 5;
  fresh->constant = false;
  fresh = function ()
  {
    this + "_" + String.counter_++;
  }
}|;


/*---------.
| System.  |
`---------*/

do (System)
{
  const var Status = do (Object.new())
  {
    function stat(var name)
    {
      clog.enabled = false |
      var v = get(name) |
      assert { v[1] == "kB"; } |
      clog.enabled = true |
      v[0]
    };
  };

  function status()
  {
    skipIfWindows();
    var procStatus = Path.new("/") / "proc" / "self" / "status";
    if (!procStatus.exists)
      testSkip("No /proc/self/status");
    var is = InputStream.new(File.new(procStatus));
    var d = [ => ];
    var line;
    while (!(line = is.getLine()).isNil())
    {
      var l = line.split(":\t");
      try {
        d[l[0]] =
          l[1]
          .split(" ", 1024, false, false)
          .map(closure (v) { try { v.asFloat() } catch (var e) { v } })
      } 
      catch (var e)
      {
        d[l[0]] = l[1]
      }
    };
    d.addProto(Status)
  };

  function memCheck(var f, var loc = nil)
  {
    var judge_mode = false;
    var location = nil;

    var begMem = nil;
    var endBenchMem = nil;
    var endJudgeMem = nil;

    function mode() { if (judge_mode) "judge" else "bench" };

    var checker = do (Tag.new())
      {
        function onEnter()
        {
          begMem = System.status().stat("VmData");
          clog << "%s: %s: start with %d kB" % [location, mode(), begMem];
        };

        function onLeave()
        {
          // FIXME: trigger the garbage collection
          // sleep(100ms);

          var endMem = System.status().stat("VmData");
          clog << "%s: %s: leak %d kB" % [location, mode(), endMem - begMem];
          if (judge_mode)
          {
            endJudgeMem = endMem;
            assert { endJudgeMem <= endBenchMem; };
          }
          else
            endBenchMem = endMem;
        };
      };

    System.memCheck = function (var f, var loc = nil)
    {
      if (loc.isNil())
        loc = Location.here(-1);
      location = loc;

      judge_mode = false;
      checker: f();
      sleep(0);
      judge_mode = true;
      checker: f();
      sleep(0);
    };

    if (loc.isNil)
      loc = Location.here(-1);
    memCheck(f, loc);
  };

  // Check that spawn threads do not map too much space.
  function memThreadCheck(var expectedMem, var f, var loc = nil)
  {
    if (loc.isNil())
      loc = Location.here(-1);

    var beg = System.status().stat("VmData") |
    clog << "before thread: %d kB" % [beg] |

    memCheck(f, loc) |

    var end = System.status().stat("VmData") |
    clog << "after thread: %d kB" % [end] |

    // Ensure that the thread does not ask for more than 32 KB.
    assert { end - beg <= expectedMem; } |
  };

}|;


/*----------.
| Windows.  |
`----------*/

// Keep sync with urbivalid/test.u.
if (System.Platform.isWindows)
{
  package lang.Process
  {
    function init(var args[])
    {
      testSkip("Process is not supported under Windows")
    };
  };

  do (System)
  {
    // System.system.
    var binshs = ["/bin/sh", "/bin/sh.exe",
                  "\\cygwin\\bin\\sh", "\\cygwin\\bin\\sh.exe",
		  "Z:\\bin\\sh"];
    for (var p : binshs)
    {
      try
      {
        if (Path.new(p).exists)
        {
          var this.binsh = p;
          break;
        };
      }
      catch {}
    };
    if (!hasLocalSlot("binsh"))
      throw Exception.new("cannot find /bin/sh: " + binshs);

    var this.count = 1;

    // On Windows, use /bin/sh to please our tests.
    overrideSlot("system",
    function (command)
    {
      // For some reason I don't understand, the command is
      // backgrounded on Windows (or by Wine).  So use some stamp file
      // to mark the end of the command.  This also provides a means
      // to get the real exit status with Wine.
      var stamp = Path.new("%s.stamp" % count) |
      ++count |
      // As a subshell, not a subcommand ({ %s; }) so that if the
      // command includes an "exit", then the stamp file is really
      // created anyway with the right value.
      command = "( %s ); echo $? >%s" % [command, stamp]|

      // Escape all the quotes and backslashes, and call /bin/sh.
      var cmd = binsh + " -c \"" |
      for| (var c: command)
        cmd += {if (c in ["\\", "\""]) "\\" else ""} + c |
      cmd += "\"" |

      // Run the command, wait for the stamp.
      'system$old'(cmd) |
      while| (!stamp.exists)
        sleep(20ms)|
      var res = File.new(stamp).asList[0].asFloat |
      clog << ("system wrapper: " + command + ": " + cmd + ": " + res) |
      res
    });
  }
}|;
