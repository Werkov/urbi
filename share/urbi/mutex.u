/*
 * Copyright (C) 2010, Gostai S.A.S.
 *
 * This software is provided "as is" without warranty of any kind,
 * either expressed or implied, including but not limited to the
 * implied warranties of fitness for a particular purpose.
 *
 * See the LICENSE file for more information.
 */

const var Global.Mutex = Tag.new;
do (Mutex)
{
  var type = "Mutex" |
  var asMutex = this |
  var asString = Object.getSlot("asString");

  function init()
  {
    var this.sem = Semaphore.new(1);
    at (enter?) lock;
    at (leave?) unlock;
  };

  function lock()
  {
    this.sem.acquire;
  };

  function unlock()
  {
    this.sem.release;
  };

  function '!'()
  {
    return NotMutex.new(this);
  };
};

var Global.NotMutex = Mutex.new;
do (NotMutex)
{
  var type = "NotMutex" |
  var asNotMutex = this |
  var asString = Object.getSlot("asString");

  function init(mtx)
  {
    var this.mtx = mtx
  };

  function clone()
  {
    var res = Mutex.new;
    res.protos = [NotMutex];
    return res;
  };

  function lock()
  {
    this.mtx.unlock()
  };

  function unlock()
  {
    this.mtx.lock()
  };
};
