## ------- ##
## Float.  ##
## ------- ##

do Float
{
  addProto(Comparable);
  addProto(Orderable);

  function init(x) { set(x) };

  function sqr() { self * self };

  function '~='(x) { (self - x).abs <= epsilontilde };
  function '%='(x) { (1.0 - self / x).abs <= epsilonpercent };
  # FIXME: =~= cannot be correctly implemented without properties.
  function '=~='(x) { (self - x) <= delta + x.delta };
  function sgn() { if (self < 0) -1 else if (self > 0) 1 else 0 };

  // Create a list 0..(self - 1).
  function seq()
  {
    var result = [] |
    for| (var i = 0; i < self; i++)
      result.push_back(i.clone) |
    result
  };

  function '\'n'()
  {
    (self - rangemin) / (rangemax - rangemin)
  };

  function getNormHook() {
    function(n, value) {
      var rmin = rangemin|
      var rmax = rangemax|
      value = value*(rmax-rmin) + rmin|
      var value.rangemin = rmin|
      var value.rangemax = rmax|
      value.setProperty("'n", "updateHook",
	Float.getNormHook.inject("parent",parent).inject("name", name) )|
      parent.changeSlot(name, value)|
    }
  };

  function newPropertyHook(parent, name, prop, value)
  {
    installUpdateHookStack(parent, name);

    function rangeminHook(slot, val)
    {
      var min = getProperty(slot, "rangemin");
      if (val < min)
        min
      else
        val
    };

    function rangemaxHook(slot, val)
    {
      var max = getProperty(slot, "rangemax");
      if (val > max)
        max
      else
        val
    };

    switch (prop)
    {
      case "rangemin":
        parent.setProperty(name, "updateHook", rangeminHook);
      case "rangemax":
        parent.setProperty(name, "updateHook", rangemaxHook);
    };
  };

  # Repeat \a action \a self times.
  function times (action)
  {
    for| (var i = 0; i < self; i++)
      action(i)
  };
};


