## ------- ##
## Float.  ##
## ------- ##

do Float
{
  addProto(Comparable);
  addProto(Orderable);

  function init(x) { set(x) };

  function sqr() { this * this };

  function '~='(x) { (this - x).abs <= epsilontilde };
  function '%='(x) { (1.0 - this / x).abs <= epsilonpercent };
  # FIXME: =~= cannot be correctly implemented without properties.
  function '=~='(x) { (this - x) <= delta + x.delta };
  function sgn() { if (this < 0) -1 else if (this > 0) 1 else 0 };

  // Create a list 0..(this - 1).
  function seq()
  {
    var result = [] |
    for| (var i = 0; i < this; i++)
      result.push_back(i.clone) |
    result
  };

  function '\'n'()
  {
    (this - rangemin) / (rangemax - rangemin)
  };

  function getNormHook() {
    function(n, value) {
      var rmin = rangemin|
      var rmax = rangemax|
      value = value*(rmax-rmin) + rmin|
      var value.rangemin = rmin|
      var value.rangemax = rmax|
      value.setProperty("'n", "updateHook",
	Float.getNormHook.inject("parent",parent).inject("name", name) )|
      parent.changeSlot(name, value)|
    }
  };

  function newPropertyHook(parent, name, prop, value)
  {
    installUpdateHookStack(parent, name);

    function rangeminHook(slot, val)
    {
      var min = getProperty(slot, "rangemin");
      if (val < min)
        min
      else
        val
    };

    function rangemaxHook(slot, val)
    {
      var max = getProperty(slot, "rangemax");
      if (val > max)
        max
      else
        val
    };

    switch (prop)
    {
      case "rangemin":
        parent.setProperty(name, "updateHook", rangeminHook);
      case "rangemax":
        parent.setProperty(name, "updateHook", rangemaxHook);
    };
  };

  # Repeat \a action \a this times.
  function times (action)
  {
    for| (var i = 0; i < this; i++)
      action(i)
  };
};


