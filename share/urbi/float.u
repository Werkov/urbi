/*--------.
| Float.  |
`--------*/

do (Float)
{
  // The connectives == and < are builtins.
  //
  // Warning: We derive from Orderable first so that we use its
  // Object.==, otherwise String would use both Comparable.== and
  // Comparable.!= that bounce endlessly one on the other.
  setProtos([Orderable, Comparable, RangeIterable]);

  function sqr() { this * this };

  /// Absolute approximation parameter.
  var epsilonTilde = 0.0001;

  /// Absolute approximation.
  function '~='(x) { (this - x).abs <= epsilonTilde };

/* %= is now a = a % b.

  /// Relative approximation parameter.
  var epsilonPercent = 0.0001;

  /// Relative approximation.
  function '%='(x) { (1.0 - this / x).abs <= epsilonPercent };
*/

  // FIXME: =~= cannot be correctly implemented without properties.
  function '=~='(x) { (this - x).abs <= delta + x.delta };

  function asList()
  {
    seq
  };

  /// Repeat \a action \a this times.
  // Not using RangeIterable to optimize: no need to build explicitly
  // the list.
  function each(action)
  {
    for (var i = 0; i < this; i += 1)
      action(i)
  };

  /// Repeat \a action \a this times separated with '|'.
  // Not using RangeIterable to optimize: no need to build explicitly
  // the list.
  function 'each|'(action)
  {
    for| (var i = 0; i < this; i += 1)
      action(i)
  };

  var pi = 3.14159265358979323846264338327950288;

  function sgn() { if (this < 0) -1 else if (this > 0) 1 else 0 };

  /// Normalized value (i.e., 0 <= res <= 1).
  function '\'n'()
  {
    (this - rangemin) / (rangemax - rangemin)
  };

  /// owner: The object that owns the slot onto which a property will be
  ///        given.
  /// name:  Slot name.
  /// prop:  Property name (e.g., "rangemin").
  /// value: Property value (e.g., 0).
  function newPropertyHook(owner, name, prop, value)
  {
    installUpdateHookStack(owner, name)|

    // Setup 'n mecanism
    {
      var slotName = name + "_quote_n"|

      if (!owner.hasSlot(slotName))
      {
        function quote_n()
        {
          var max = owner.getProperty(name, "rangemax") |
          var min = owner.getProperty(name, "rangemin") |
          var val = owner.getSlot(name) |
          return (val - min) / (max - min) |
        }|

        function hook(slot, val)
        {
          var max = owner.getProperty(name, "rangemax") |
          var min = owner.getProperty(name, "rangemin") |
          updateSlot(name, min + (max - min) * val) |
          return |
        }|

        owner.setSlot(slotName, quote_n)|
        owner.setProperty(slotName, "updateHook", hook)|
      }|
    }|

    function rangeminHook(slot, val)
    {
      var min = getProperty(slot, "rangemin");
      if (val < min)
        min
      else
        val
    }|

    function rangemaxHook(slot, val)
    {
      var max = getProperty(slot, "rangemax");
      if (val > max)
        max
      else
        val
    }|

    switch (prop)
    {
      case "rangemin":
        owner.setProperty(name, "updateHook", rangeminHook)|
      case "rangemax":
        owner.setProperty(name, "updateHook", rangemaxHook)|
    }|
  };

  // Repeat \a action \a this times.
  function times (action)
  {
    for| (var i = 0; i < this; i++)
      action()
  };

  function map(f)
  {
    asList.map(f)
  };

  function hex()
  {
    format(FormatInfo.new("%x"))
  };

};


