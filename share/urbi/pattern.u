/*
 * Copyright (C) 2010, Gostai S.A.S.
 *
 * This software is provided "as is" without warranty of any kind,
 * either expressed or implied, including but not limited to the
 * implied warranties of fitness for a particular purpose.
 *
 * See the LICENSE file for more information.
 */

class Global.Pattern
{
  var pattern;
  var bindings = [ => ];

  function init(p)
  {
    pattern = p |
    bindings = [ => ] |
  };

  function match(value)
  {
    return matchPattern(pattern, value) |
  };

  function matchPattern(pattern, value)
  {
    if (pattern.hasSlot("match"))
      return pattern.match(this, value)
    else if (value.hasSlot("matchAgainst"))
      return value.matchAgainst(this, pattern)
    else
      return value == pattern |
  };

  class Binding
  {
    var name = "";

    function init(n)
    {
      name = n |
    };

    function asString()
    {
      return "var " + name |
    };

    function match(pattern, value)
    {
      pattern.bindings[name] = value |
      return true |
    };
  };
};
