class Global.Exception
{
  var message;
  var backtrace;
  var location;

  function init(msg)
  {
    message = msg
  };

  function asString()
  {
    var res = message |
    if (!location.empty)
      res = "%s: %s" % [location, res] |
    return res
  };

  function asPrintable()
  {
    "%s `%s'" % [type, message]
  };
};

class Global.PrimitiveError: Exception
{
  var routine;

  function init(rout, msg)
  {
    Exception.getSlot("init").apply([this, "%s: %s" % [rout, msg]])
  };
};

class Global.ArityError: Exception
{
  var routine;
  var effective;
  var minimum;
  var maximum;

  function init
  {
    // FIXME: a range of argument should be handled with a range
    // object (1..3)
    var args = call.evalArgs |

    if (args.size not in [3, 4])
      // Throw
      {} |

    var plural = "" |

    routine = args[0] |
    effective = args[1] |
    minimum = args[2] |

    switch (args.size)
    {
      case 3:
        if (args[2] > 1)
          plural = "s"|
        var msg = "%s: Expected %s argument%s, given %s"|
        Exception.getSlot("init").apply
          ([this, msg % [routine, minimum, plural, effective]])|
      case 4:
        maximum = args[3]|
        if (maximum > 1)
          plural = "s"|
        var msg = "%s: Expected between %s and %s argument%s, given %s"|
        Exception.getSlot("init").apply
          ([this, msg % [routine, minimum, maximum, plural, effective]])|
    }
  }
};

class Global.BadIntegerError: Exception
{
  var routine;
  var effective;

  function init(rout, fmt, eff)
  {
    routine = rout |
    effective = eff |
    Exception.getSlot("init").apply([this, "%s: %s" % [rout, fmt % [eff]]])
  };
};

class Global.TypeError: Exception
{
  var expected;
  var effective;

  function init(exp, eff)
  {
    expected = exp |
    effective = eff |
    message = "unexpected %s, expected a %s" %
    [eff.asPrintable, exp.type]
  }
};

class Global.ArgumentTypeError: TypeError
{
  var index;
  var routine;

  function init(rout, idx, exp, eff)
  {
    TypeError.getSlot("init").apply([this, exp, eff]) |
    routine = rout |
    index = idx |
    message = "%s: unexpected %s for argument %s, expected a %s" %
    [routine, eff.asPrintable, idx, exp.type]
  }
};

class Global.LookupError: Exception
{
  var msg;
  var obj;

  var fixSpellingErrors = true;

  function init(m, o)
  {
    msg = m |
    obj = o |
    var explanation = "Lookup failed: %s" % [m] |
    var closest = nil |
    if (fixSpellingErrors)
      closest = mispelling(m, o) |
    if (closest)
      explanation += " (possible mispelling of %s)" % [closest] |
    Exception.getSlot("init").apply([this, explanation])
  };

  // Return the most probable slot in "o" (recursively) ressembling "m"
  // or "nil" if we have no satisfying answer.
  function mispelling(m, o)
  {
    // One character is too short.
    if (m.size == 1)
      return nil |

    var lcm = m.toLower |
    var allNames = o.allSlotNames |
    var min = 999 |
    var closest = nil |
    for| (var n: allNames) {
      var d = lcm.distance(n.toLower) |
      if (d < min) {
        min = d |
        closest = n |
	if (min == 0)      // Case mismatch only, that must be it.
          return closest
      }
    } |
    // Do not overcorrect. Two errors, not including case mismatches,
    // ought to be enough for everybody. Also, do not try to correct
    // words with less than three letters unless we got a case
    // mismatch (caught above) or only one case-sensitive change.
    if (min > 2 || (m.size < 3 && closest.size < 3 && m.distance(closest) > 1))
      return nil |
    closest
  }
};

class Global.RedefinitionError: Exception
{
  var slot;

  function init(s)
  {
    slot = s |
    var explanation = "slot redefinition: %s" % [s] |
    Exception.getSlot("init").apply([this, explanation])
  }
};
