class Global.Exception
{
  var message;
  var backtrace;
  var location;

  function init(msg)
  {
    message = msg
  };

  function asString()
  {
    var res = message;
    if (!location.empty)
      res = "%s: %s" % [location, res];
    return res;
  };

  function asPrintable()
  {
    "%s `%s'" % [protoName, message]
  };
};

class Global.ArityError: Exception
{
  var effective;
  var minimum;
  var maximum;

  function init
  {
    // FIXME: a range of argument should be handled with a range
    // object (1..3)
    var args = call.evalArgs;

    if (args.size not in [2, 3])
      // Throw
      {};

    var plural = "";

    effective = args[0];
    minimum = args[1];

    switch (args.size)
    {
      case 2:
        if (args[1] > 1)
          plural = "s";
        var msg = "Expected %s argument%s, given %s";
        Exception.getSlot("init").apply
          ([this, msg % [minimum, plural, effective]]);
      case 3:
        maximum = args[2];
        if (maximum > 1)
          plural = "s";
        var msg = "Expected between %s and %s argument%s, given %s";
        Exception.getSlot("init").apply
          ([this, msg % [minimum, maximum, plural, effective]]);
    };
  }
};

class Global.TypeError: Exception
{
  var expected;
  var effective;

  function init(exp, eff)
  {
    expected = exp;
    effective = eff;
    message = "unexpected `%s', expected `%s'" %
    [eff.protoName, exp.protoName]
  }
};

class Global.ArgumentTypeError: TypeError
{
  var index;

  function init(idx, exp, eff)
  {
    TypeError.getSlot("init").apply([this, exp, eff]);
    index = idx;
    message = "unexpected `%s' for argument %s, expected `%s'" %
    [eff.protoName, idx, exp.protoName]
  }
};
