/*
 * Copyright (C) 2011, Gostai S.A.S.
 *
 * This software is provided "as is" without warranty of any kind,
 * either expressed or implied, including but not limited to the
 * implied warranties of fitness for a particular purpose.
 *
 * See the LICENSE file for more information.
 */

requireFile("urbi/loadable.u");
requireFile("urbi/call-message.u");
requireFile("urbi/channel.u");
requireFile("urbi/global.u");
requireFile("urbi/exception.u");
requireFile("urbi/control.u");

/*-----------------------.
| Profiling operations.  |
`-----------------------*/

class Global.Profile
{
  class Function
  {
    function asString()
    {
      "%s('%s', %s, %.6f, %.6f)"
      % [type, name, calls, selfTime, selfTimePer]
    };
  };

  function asString()
  {
    var str = "Profile(\n"
              "  Yields: %s\n"
              "  Total time: %.6fs\n"
              "  Wall clock time: %.6fs\n"
              "  Function calls: %s\n"
              "  Max function call depth: %s\n\n"
              % [yields, totalTime, wallClockTime,
                 totalCalls, maxFunctionCallDepth] |

    str += "  .--------------------------------------------------------.\n"
           "  |   Function   |  Calls  |   Self time   | Self time per |\n"
           "  |--------------+---------+---------------+---------------|\n" |

    var totalSelfTimes = 0 |
    var totalSelfTimePers = 0 |
    for (var c: calls)
    {
      str += "  | %12s | %7s | %13.6f | %13.6f |\n"
               % [c.name, c.calls, c.selfTime, c.selfTimePer] |
      totalSelfTimes    += c.selfTime |
      totalSelfTimePers += c.selfTimePer |
    } |

    str += "  |--------------+---------+---------------+---------------|\n"
           "  | %12s | %7s | %13.6f | %13.6f |\n"
           "  '--------------'---------'---------------'---------------'\n)\n"
           % ["Totals", totalCalls, totalSelfTimes, totalSelfTimePers]
  };
};
