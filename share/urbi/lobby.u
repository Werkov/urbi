/*
 * Copyright (C) 2010, Gostai S.A.S.
 *
 * This software is provided "as is" without warranty of any kind,
 * either expressed or implied, including but not limited to the
 * implied warranties of fitness for a particular purpose.
 *
 * See the LICENSE file for more information.
 */

var Lobby.systemLobby = this;
do (Lobby)
{
  var onDisconnect = Event.new;
  var connected = true;

  function asString()
  {
    var res = Object.getSlot("asString").apply([this]) |
    if (hasSlot("remoteIP"))
      res += " (%s)" % remoteIP |
    return res
  };

  function echo
  {
    var tag = ""|
    if (1 < call.argsCount)
      tag = call.evalArgAt(1)|
    send("*** " + call.evalArgAt(0).asString, tag)
  };

  function handleDisconnect()
  {
    connected = false |
    Lobby.systemLobby.echo("disconnecting " + this) |
    // Break the reference loop of notifyChange local to this lobby.
    if (hasLocalSlot("handle") && handle.isA(Finalizable))
      removeSlot("handle")|
    onDisconnect!(this)
  };

  // Ready to go.  The "Urbi is up..." string is special and expected
  // by uconsole-check as the signal that we have loaded urbi.u.  It
  // ignores messages before this one, this one included.
  function resendBanner()
  {
    (System
      .PackageInfo
      .components["Urbi SDK"]
      .banner
      .split("\n")
      << "Urbi is up and running.")
      .each(closure (s) { echo(s, "start") })
  };

  function wall
  {
    call.message = "echo";
    for (var l in lobbies - [Lobby])
      l.callMessage(call);
  };
};
